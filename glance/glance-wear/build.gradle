/*
 * Copyright 2021 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.LibraryGroups
import androidx.build.LibraryType
import androidx.build.Publish
import androidx.build.RunApiTasks
import androidx.build.AndroidXComposePlugin

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

// Disable multi-platform; this will only be used on Android.
AndroidXComposePlugin.applyAndConfigureKotlinPlugin(project, /* isMultiplatformEnabled= */false)

dependencies {
    kotlinPlugin(project(":compose:compiler:compiler"))

    api(project(":glance:glance"))
    api(project(":wear:tiles:tiles"))
    api("androidx.compose.runtime:runtime:1.0.1")

    implementation(libs.kotlinStdlib)
    implementation(libs.kotlinCoroutinesGuava)
    implementation "androidx.lifecycle:lifecycle-runtime:2.3.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-service:2.3.1"

    testImplementation(libs.testRules)
    testImplementation(libs.testRunner)
    testImplementation(libs.truth)
    testImplementation(libs.junit)
    testImplementation(libs.kotlinCoroutinesTest)
    testImplementation(libs.robolectric)
    // TODO(b/197638718): Remove when tiles-testing dependencies are fixed.
    testImplementation(project(":wear:tiles:tiles-renderer"))
    testImplementation(project(":wear:tiles:tiles-testing"))

    androidTestImplementation(project(":core:core"))
    androidTestImplementation(project(":test:screenshot:screenshot"))
    androidTestImplementation(project(":wear:tiles:tiles-renderer"))
    androidTestImplementation(libs.testExtJunit)
    androidTestImplementation(libs.testCore)
    androidTestImplementation(libs.testRunner)
    androidTestImplementation(libs.testRules)
    androidTestImplementation(libs.kotlinCoroutinesTest)

    // I'm not 100% sure why, but androidTestImplementation doesn't appear to use the standard
    // results of a project build. This leads to it not using the shadow configuration from
    // tiles-proto, and thus failing to find the protobuf classes at runtime.
    //
    // This line forces the androidTest to use the properly shaded proto library on the runtime
    // classpath.
    androidTestRuntimeOnly(project(path: ":wear:tiles:tiles-proto", configuration: "shadow"))
}

android {
    defaultConfig {
        minSdkVersion 26
    }
    // Use Robolectric 4.+
    testOptions.unitTests.includeAndroidResources = true

    lintOptions {
        // TODO(b/191286558): Remove when lint is fixed.
        disable "UnsafeOptInUsageError", "UnsafeOptInUsageWarning"
    }

    sourceSets {
        androidTest.assets.srcDirs += project.rootDir.absolutePath + "/../../golden/glance/glance-wear"
    }
}

androidx {
    name = "Glance for Wear Tiles"
    type = LibraryType.PUBLISHED_LIBRARY
    publish = Publish.SNAPSHOT_ONLY
    mavenGroup = LibraryGroups.GLANCE
    inceptionYear = "2021"
    description = "Glance allows developers to build layouts for Wear Tiles using a Jetpack " +
            "Compose-style API."
    targetsJavaConsumers = false
}
