import static androidx.build.dependencies.DependenciesKt.*
import androidx.build.LibraryGroups
import androidx.build.LibraryVersions
import androidx.build.Publish

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
}

dependencies {
    api("androidx.annotation:annotation:1.1.0")
    // TODO: change to 1.1.0-alpha04 after release
    api("androidx.core:core:1.1.0-rc01")
    implementation("androidx.collection:collection:1.0.0")
    api("androidx.customview:customview:1.0.0")

    androidTestImplementation(ANDROIDX_TEST_EXT_JUNIT)
    androidTestImplementation(ANDROIDX_TEST_CORE)
    androidTestImplementation(ANDROIDX_TEST_RUNNER)
    androidTestImplementation(ANDROIDX_TEST_RULES)
    androidTestImplementation(ESPRESSO_CORE, libs.exclude_for_espresso)
    androidTestImplementation(ESPRESSO_CONTRIB, libs.exclude_for_espresso)
    androidTestImplementation(MOCKITO_CORE, libs.exclude_bytebuddy)
    androidTestImplementation(DEXMAKER_MOCKITO, libs.exclude_bytebuddy) // DexMaker has it"s own MockMaker
    androidTestImplementation project(':internal-testutils'), {
        exclude group: 'androidx.coordinatorlayout', module: 'coordinatorlayout'
    }
}

android {
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7
    }
    sourceSets {
        main.res.srcDirs = [
                'src/main/res',
                'src/main/res-public'
        ]
    }
    buildTypes.all {
        consumerProguardFiles 'proguard-rules.pro'
    }
}

androidx {
    name = "Android Support Library Coordinator Layout"
    publish = Publish.SNAPSHOT_AND_RELEASE
    mavenVersion = LibraryVersions.COORDINATORLAYOUT
    mavenGroup = LibraryGroups.COORDINATORLAYOUT
    inceptionYear = "2011"
    description = "The Support Library is a static library that you can add to your Android application in order to use APIs that are either not available for older platform versions or utility APIs that aren't a part of the framework APIs. Compatible on devices running API 14 or later."
    failOnDeprecationWarnings = false
}
