/*
 * Copyright (C) 2017 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import android.support.LibraryVersions
import android.support.SupportLibraryExtension

plugins {
    id("SupportAndroidLibraryPlugin")
    id("kotlin-android")
}

android {
    defaultConfig {
        minSdkVersion flatfoot.min_sdk
    }
}

dependencies {
    compile project(":arch:runtime")
    compile project(":paging:common")
    compile project(":lifecycle:runtime")
    compile project(':lifecycle:extensions')

    compile libs.support.recyclerview, libs.support_exclude_config

    androidTestImplementation libs.junit
    androidTestImplementation libs.mockito_core,     { exclude group: 'net.bytebuddy' } // DexMaker has it"s own MockMaker
    androidTestImplementation libs.dexmaker_mockito, { exclude group: 'net.bytebuddy' } // DexMaker has it"s own MockMaker
    androidTestImplementation libs.test_runner,      { exclude module: 'support-annotations' }
    androidTestImplementation libs.espresso_core,    { exclude module: 'support-annotations' }
    androidTestImplementation libs.kotlin.stdlib

}

createAndroidCheckstyle(project)
createKotlinCheckstyle(project)

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    def suffix = name.capitalize()
    project.tasks.create(name: "jar${suffix}", type: Jar){
        dependsOn variant.javaCompile
        from variant.javaCompile.destinationDir
        destinationDir new File(project.buildDir, "libJar")
    }
}

version = LibraryVersions.PAGING.toString()
supportLibrary {
    name 'Android Lifecycle Extensions'
    publish true
    inceptionYear '2017'
    description "Android Lifecycle Extensions"
    url SupportLibraryExtension.ARCHITECTURE_URL
}