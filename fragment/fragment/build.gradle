import static androidx.build.dependencies.DependenciesKt.*
import androidx.build.LibraryGroups
import androidx.build.LibraryVersions
import androidx.build.Publish

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("kotlin-android")
}

android {
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    buildTypes.all {
        consumerProguardFiles "proguard-rules.pro"
    }

    defaultConfig {
        multiDexEnabled true
    }
}

dependencies {
    api("androidx.annotation:annotation:1.1.0")
    api("androidx.core:core:1.2.0")
    api("androidx.collection:collection:1.1.0")
    api("androidx.viewpager:viewpager:1.0.0")
    api("androidx.loader:loader:1.0.0")
    api("androidx.activity:activity:1.2.2")
    api("androidx.lifecycle:lifecycle-livedata-core:2.3.1")
    api("androidx.lifecycle:lifecycle-viewmodel:2.3.1")
    api("androidx.lifecycle:lifecycle-viewmodel-savedstate:2.3.1")
    api("androidx.savedstate:savedstate:1.1.0")
    api("androidx.annotation:annotation-experimental:1.0.0")

    androidTestImplementation("androidx.appcompat:appcompat:1.1.0", {
        exclude group: "androidx.fragment", module: "fragment"
        exclude group: "androidx.activity", module: "activity"
    })
    androidTestImplementation(KOTLIN_STDLIB)
    androidTestImplementation(ANDROIDX_TEST_EXT_JUNIT)
    androidTestImplementation(ANDROIDX_TEST_CORE)
    androidTestImplementation(ANDROIDX_TEST_RUNNER)
    androidTestImplementation(ANDROIDX_TEST_RULES)
    androidTestImplementation(TRUTH)
    androidTestImplementation(ESPRESSO_CORE, excludes.espresso)
    androidTestImplementation(MOCKITO_CORE, excludes.bytebuddy) // DexMaker has it"s own MockMaker
    androidTestImplementation(DEXMAKER_MOCKITO, excludes.bytebuddy) // DexMaker has it"s own MockMaker
    androidTestImplementation(MULTIDEX)
    androidTestImplementation(project(":internal-testutils-runtime"), {
        exclude group: "androidx.fragment", module: "fragment"
    })

    testImplementation(project(":fragment:fragment"))
    testImplementation(KOTLIN_STDLIB)
    testImplementation(ANDROIDX_TEST_EXT_JUNIT)
    testImplementation(ANDROIDX_TEST_CORE)
    testImplementation(ANDROIDX_TEST_RULES)
    testImplementation(TRUTH)
    testImplementation(ROBOLECTRIC)

    lintPublish(project(":fragment:fragment-lint"))
}

androidx {
    name = "Android Support Library fragment"
    publish = Publish.SNAPSHOT_AND_RELEASE
    mavenGroup = LibraryGroups.FRAGMENT
    inceptionYear = "2011"
    description = "The Support Library is a static library that you can add to your Android application in order to use APIs that are either not available for older platform versions or utility APIs that aren\'t a part of the framework APIs. Compatible on devices running API 14 or later."
}
