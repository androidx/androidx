/*
 * Copyright 2022 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */
import androidx.build.LibraryType

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("org.jetbrains.kotlin.android")
}

dependencies {
    api(libs.kotlinStdlib)
    api(libs.junit)
    implementation(libs.googletest)
    androidTestImplementation(libs.testExtJunit)
    androidTestImplementation(libs.truth)
    androidTestImplementation(libs.testCore)
    androidTestImplementation(libs.testRunner)
    androidTestImplementation(libs.espressoCore)
}

androidx {
    name = "JUnit GTest"
    type = LibraryType.PUBLISHED_LIBRARY
    inceptionYear = "2022"
    description = "Run GTest tests on Android devices"
    legacyDisableKotlinStrictApiMode = true
    // Bypassed to better match existing androidx.test libraries
    bypassCoordinateValidation = true
}

android {
    defaultConfig {
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }
    externalNativeBuild {
        cmake {
            version libs.versions.cmake.get()
            path "src/main/cpp/CMakeLists.txt"
        }
        experimentalProperties["prefab.junit-gtest.exportLibraries"] = [
                "-uJava_androidx_test_ext_junitgtest_GtestRunner_initialize",
                "-uJava_androidx_test_ext_junitgtest_GtestRunner_run",
                "-uJava_androidx_test_ext_junitgtest_GtestRunner_addTest"
        ]
    }

    buildFeatures {
        prefab = true
        prefabPublishing true
    }

    prefab {
        junitgtest {
            name "junit-gtest"
        }
    }
    namespace "androidx.test.ext.junitgtest"

    packagingOptions {
        jniLibs {
            excludes += "**/libc++_shared.so"
        }
    }
}
