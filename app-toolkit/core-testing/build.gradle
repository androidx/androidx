/*
 * Copyright (C) 2017 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import android.support.LibraryGroups
import android.support.LibraryVersions
import android.support.SupportLibraryExtension

plugins {
    id("SupportAndroidLibraryPlugin")
}

android {
    defaultConfig {
        minSdkVersion flatfoot.min_sdk
    }
}

dependencies {
    compile project(":arch:runtime")
    compile libs.support.annotations
    compile(libs.junit)
    compile libs.mockito_core, { exclude group: 'net.bytebuddy' }

    testCompile libs.junit
    testCompile libs.support.annotations

    androidTestCompile libs.junit
    androidTestImplementation libs.test_runner,      { exclude module: 'support-annotations' }
    androidTestImplementation libs.espresso_core,    { exclude module: 'support-annotations' }
}

createAndroidCheckstyle(project)

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    def suffix = name.capitalize()
    project.tasks.create(name: "jar${suffix}", type: Jar) {
        dependsOn variant.javaCompile
        from variant.javaCompile.destinationDir
        destinationDir new File(project.buildDir, "libJar")
    }
}

version = LibraryVersions.ARCH_CORE_TESTING.toString()
supportLibrary {
    name = "Android Core-Testing"
    publish = true
    mavenGroup = LibraryGroups.ARCH_CORE
    inceptionYear = "2017"
    description = "Android Core-Testing"
    url = SupportLibraryExtension.ARCHITECTURE_URL
}