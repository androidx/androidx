/*
 * Copyright (C) 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

def init = new Properties()
ext.init = init

def chooseOutDir(subdir = "") {
    /*
     * The OUT_DIR is a temporary directory you can use to put things during the build.
     */
    def outDir = System.env.OUT_DIR
    if (outDir == null) {
        def checkoutRoot = System.getProperty("CHECKOUT_ROOT")
        if (checkoutRoot == null) {
            def workingDir = new File(buildscript.sourceFile.parent)
            // Note: Using String.execute() causes exit code 148, so we use ProcessBuilder instead.
            def repoProcess = new ProcessBuilder()
                .command("repo", "help")
                .directory(workingDir)
                .start()
            def isRepoCheckout = repoProcess.waitFor() == 0
            if (isRepoCheckout) {
                checkoutRoot = new File("${buildscript.sourceFile.parent}/../../..")
            } else {
                checkoutRoot = new File("git rev-parse --show-toplevel".execute([], workingDir).text)
            }

        }
        outDir = new File("${checkoutRoot}/out${subdir}")
        project.ext.buildSrcOut = new File("${checkoutRoot}/out/buildSrc")
    } else {
        outDir = new File(outDir)
        project.ext.buildSrcOut = new File("${outDir}/buildSrc")
    }
    project.ext.outDir = outDir
    buildDir = new File(outDir, "$project.name/build")
                .getCanonicalFile()
    subprojects {
        // Change buildDir first so that all plugins pick up the new value.
        project.buildDir = new File("$project.parent.buildDir/../$project.name/build")
    }
}

ext.init.chooseOutDir = this.&chooseOutDir
