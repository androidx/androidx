apply plugin: android.support.SupportLibraryPlugin
archivesBaseName = 'recyclerview-v7'

dependencies {
    compile project(':support-annotations')
    compile project(':support-compat')
    compile project(':support-core-ui')

    androidTestCompile (libs.test_runner) {
        exclude module: 'support-annotations'
    }
    androidTestCompile (libs.espresso_core) {
        exclude module: 'support-annotations'
    }
    androidTestCompile libs.junit
    androidTestCompile libs.mockito_core
    androidTestCompile libs.dexmaker_mockito
    androidTestCompile project(':support-testutils')

    testCompile libs.junit
    testCompile libs.mockito_core
    testCompile ("$libs.test_runner") {
        exclude module: 'support-annotations'
    }
}

android {
    defaultConfig {
        minSdkVersion 14
    }

    sourceSets {
        main.java.srcDir 'src'
        main.res.srcDirs 'res', 'res-public'

        test.java.srcDir 'jvm-tests/src'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    buildTypes.all {
        consumerProguardFiles 'proguard-rules.pro'
    }
}

supportLibrary {
    name 'Android Support RecyclerView v7'
    inceptionYear '2014'
    description 'Android Support RecyclerView v7'
}
