/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.LibraryGroups
import androidx.build.LibraryVersions
import androidx.build.AndroidXExtension

import static androidx.build.dependencies.DependenciesKt.*
import androidx.build.Publish

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
}

android {
    buildTypes.all {
        consumerProguardFiles "proguard-rules.pro"
    }
}

dependencies {
    api("androidx.annotation:annotation:1.1.0")
    api(project(":hilt:hilt-common"))
    api("androidx.work:work-runtime:2.3.4")
    api(libs.hiltAndroid)
    annotationProcessor(libs.hiltCompiler)
}

android.libraryVariants.all { variant ->
    def name = variant.name
    def suffix = name.capitalize()

    // Create jar<variant> task for testImplementation in hilt-compiler.
    project.tasks.register("jar${suffix}", Jar).configure {
        dependsOn(variant.javaCompileProvider)
        from(variant.javaCompileProvider.map { task -> task.destinationDir})
        destinationDirectory.fileValue(new File(project.buildDir, "libJar"))
    }
}

androidx {
    name = "Android Lifecycle WorkManager Hilt Extension"
    publish = Publish.SNAPSHOT_AND_RELEASE
    mavenVersion = LibraryVersions.HILT
    mavenGroup = LibraryGroups.HILT
    inceptionYear = "2020"
    description = "Android Lifecycle WorkManager Hilt Extension"
}
