/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */
import androidx.build.LibraryType

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("androidx.stableaidl")
}

android {
    buildFeatures {
        aidl = true
    }
    buildTypes.configureEach {
        stableAidl {
            version 1
        }
    }
    testOptions.unitTests.includeAndroidResources = true
    namespace "androidx.browser"
}

dependencies {
    api("androidx.core:core:1.10.0")
    api("androidx.annotation:annotation:1.8.1")
    api("androidx.annotation:annotation-experimental:1.4.1")
    api(libs.guavaListenableFuture)

    implementation("androidx.activity:activity:1.9.0")
    implementation("androidx.collection:collection:1.4.2")
    implementation("androidx.concurrent:concurrent-futures:1.0.0")
    implementation("androidx.interpolator:interpolator:1.0.0")

    annotationProcessor(libs.nullaway)

    testImplementation(libs.testCore)
    testImplementation(libs.testRunner)
    testImplementation(libs.junit)
    testImplementation(libs.robolectric)
    testImplementation(libs.mockitoCore4)

    androidTestImplementation("androidx.appcompat:appcompat:1.1.0")
    androidTestImplementation(libs.testExtJunit)
    androidTestImplementation(libs.testCore)
    androidTestImplementation(libs.testRunner)
    androidTestImplementation(libs.testRules)
    androidTestImplementation(libs.espressoCore, excludes.espresso)
    androidTestImplementation(libs.mockitoCore, excludes.bytebuddy) // DexMaker has it"s own MockMaker
    androidTestImplementation(libs.dexmakerMockito, excludes.bytebuddy) // DexMaker has it"s own MockMaker
    androidTestImplementation(project(":internal-testutils-runtime"))
}

androidx {
    name = "Browser"
    type = LibraryType.PUBLISHED_LIBRARY
    inceptionYear = "2015"
    description = "Provides support for embedding Custom Tabs in an app."
}
