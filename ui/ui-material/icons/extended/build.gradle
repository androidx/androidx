/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.LibraryGroups
import androidx.build.LibraryVersions
import androidx.build.Publish
import androidx.ui.material.icons.generator.tasks.IconGenerationTask
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import static androidx.build.dependencies.DependenciesKt.*

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXUiPlugin")
    id("org.jetbrains.kotlin.android")
}

dependencies {
    kotlinPlugin project(path: ":compose:compose-compiler")
    implementation(KOTLIN_STDLIB)

    api project(":ui:ui-material-icons-core")
    implementation project(":compose:compose-runtime")

    androidTestImplementation project(":ui:ui-foundation")
    androidTestImplementation project(":ui:ui-layout")
    androidTestImplementation project(":ui:ui-core")
    androidTestImplementation project(":ui:ui-test")

    androidTestImplementation(ANDROIDX_TEST_RULES)
    androidTestImplementation(ANDROIDX_TEST_RUNNER)
    androidTestImplementation(JUNIT)
    androidTestImplementation(KOTLIN_REFLECT)
    androidTestImplementation(TRUTH)
}

android.buildTypes.all {
    // Jacoco hangs intermittently due to the large number of files
    testCoverageEnabled = false
}

IconGenerationTask.registerExtendedIconProject(project, android)

androidx {
    name = "AndroidX Extended Material Icons"
    publish = Publish.SNAPSHOT_AND_RELEASE
    mavenVersion = LibraryVersions.UI
    mavenGroup = LibraryGroups.UI
    // This module has a large number (5000+) of generated source files and so doc generation /
    // API tracking will simply take too long
    toolingProject = true
    inceptionYear = "2020"
    description = "AndroidX Extended Material Icons"
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        useIR = true
    }
}
