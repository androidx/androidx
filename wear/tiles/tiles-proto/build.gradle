/*
 * Copyright 2021 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.LibraryType
import androidx.build.RunApiTasks

plugins {
    id("AndroidXPlugin")
    id("AndroidXRepackagePlugin")
    id("java-library")
    id("com.google.protobuf")
}

repackage {
    // Must match what is in wear/protolayout/protolayout-external-protobuf/build.gradle
    addRelocation {
        sourcePackage = "com.google.protobuf"
        targetPackage =  "androidx.wear.protolayout.protobuf"
    }
}

dependencies {
    constraints {
        api(project(":wear:protolayout:protolayout-proto"))
    }
    implementation("androidx.annotation:annotation:1.8.1")
    api(project(":wear:protolayout:protolayout-proto"))
    // Must be compileOnly to not bring in protobufLite in runtime
    // Repackaged protobufLite brought in by
    // project(":wear:protolayout:protolayout-external-protobuf") is used at runtime
    compileOnly(libs.protobufLite)
}

protobuf {
    protoc {
        artifact = libs.protobufCompiler.get()
    }
    generateProtoTasks {
        ofSourceSet("main").each { task ->
            project.sourceSets.main.java.srcDir(task)
        }
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}
afterEvaluate {
    lint {
        lintOptions {
            // protobuf generates unannotated methods
            disable("UnknownNullness")
        }
    }
}
androidx {
    name = "Wear Tiles Proto"
    type = LibraryType.PUBLISHED_LIBRARY
    inceptionYear = "2020"
    description = "Jarjar the generated proto and proto-lite dependency for use by " +
            "wear-tiles."
    runApiTasks = new RunApiTasks.No("Metalava doesn't properly parse the proto sources " +
            "(b/180579063)")
}