/*
 * Copyright 2021 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */
import androidx.build.LibraryType
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
    id("org.jetbrains.kotlin.android")
}

dependencies {
    api("androidx.compose.foundation:foundation:1.7.0")
    api("androidx.compose.ui:ui:1.7.0")
    api("androidx.compose.ui:ui-text:1.7.0")
    api("androidx.compose.runtime:runtime:1.7.0")
    api(project(":wear:compose:compose-foundation"))

    implementation(libs.kotlinStdlib)
    implementation(libs.kotlinCoroutinesCore)
    implementation("androidx.compose.animation:animation:1.7.0")
    implementation("androidx.compose.material:material-icons-core:1.7.0")
    implementation("androidx.compose.material:material-ripple:1.7.0")
    implementation("androidx.compose.ui:ui-util:1.7.0")
    implementation(project(":wear:compose:compose-material-core"))
    implementation("androidx.profileinstaller:profileinstaller:1.3.1")
    implementation("androidx.graphics:graphics-shapes:1.0.0-beta01")

    androidTestImplementation(project(":compose:ui:ui-test"))
    androidTestImplementation(project(":compose:ui:ui-test-junit4"))
    androidTestImplementation(project(":compose:test-utils"))
    androidTestImplementation(project(":test:screenshot:screenshot"))
    androidTestImplementation(libs.testParameterInjector)
    androidTestImplementation(libs.testRunner)
    androidTestImplementation(libs.truth)
    androidTestImplementation(project(":wear:compose:compose-material3-samples"))

    testImplementation(libs.testRules)
    testImplementation(libs.testRunner)
    testImplementation(libs.junit)
}

android {
    compileSdk 35

    defaultConfig {
        minSdkVersion 25
    }
    // Use Robolectric 4.+
    testOptions.unitTests.includeAndroidResources = true
    sourceSets.androidTest.assets.srcDirs +=
            project.rootDir.absolutePath + "/../../golden/wear/compose/material3"
    buildTypes.configureEach {
        consumerProguardFiles("proguard-rules.pro")
    }
    namespace "androidx.wear.compose.material3"
    lint {
        baseline = file("lint-baseline.xml")
    }
}

androidx {
    name = "Android Wear Compose Material 3"
    type = LibraryType.PUBLISHED_LIBRARY_ONLY_USED_BY_KOTLIN_CONSUMERS
    mavenVersion = LibraryVersions.WEAR_COMPOSE_MATERIAL3
    inceptionYear = "2022"
    description = "WearOS Compose Material 3 Library. This library makes it easier for " +
            "developers to write Jetpack Compose applications for Wearable devices that " +
            "implement Wear Material 3 Design UX guidelines and specifications. It builds upon " +
            "the Jetpack Compose libraries."
    legacyDisableKotlinStrictApiMode = true
    metalavaK2UastEnabled = false
    samples(project(":wear:compose:compose-material3-samples"))
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += [
                "-Xjvm-default=all",
        ]
    }
}
