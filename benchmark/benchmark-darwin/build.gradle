import androidx.build.Publish
import org.jetbrains.kotlin.gradle.plugin.mpp.BitcodeEmbeddingMode
import org.jetbrains.kotlin.gradle.plugin.mpp.apple.XCFrameworkConfig

plugins {
    id("AndroidXPlugin")
}

androidXMultiplatform {
    def xcf = new XCFrameworkConfig(project, "AndroidXDarwinBenchmarks")
    ios {
        binaries.framework {
            baseName = "AndroidXDarwinBenchmarks"
            // https://youtrack.jetbrains.com/issue/KT-48552
            embedBitcode = BitcodeEmbeddingMode.DISABLE
            xcf.add(it)
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                api(libs.kotlinStdlib)
            }
        }
        commonTest {
            dependencies {
                implementation(libs.kotlinTest)
                implementation(libs.kotlinTestAnnotationsCommon)
            }
        }
        iosArm64Main {
            dependsOn(commonMain)
            dependencies {
                api(project(":benchmark:benchmark-darwin-core"))
            }
        }
        iosSimulatorArm64Main {
            dependsOn(iosArm64Main)
        }
        iosX64Main {
            dependsOn(iosArm64Main)
        }
    }
}

androidx {
    name = "Benchmarks - Darwin"
    inceptionYear = "2022"
    description = "AndroidX Benchmarks - Darwin"
    publish = Publish.SNAPSHOT_ONLY
}
