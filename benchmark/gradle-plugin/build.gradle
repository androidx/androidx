/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.BuildServerConfigurationKt
import androidx.build.CompilationTarget
import androidx.build.LibraryGroups
import androidx.build.LibraryVersions
import androidx.build.Publish
import androidx.build.SdkResourceGenerator

import static androidx.build.dependencies.DependenciesKt.*

plugins {
    id("AndroidXPlugin")
    id("kotlin")
    id("java-gradle-plugin")
}

apply from: "../../buildSrc/kotlin-dsl-dependency.gradle"

dependencies {
    implementation findGradleKotlinDsl()
    implementation gradleApi()
    implementation("com.android.tools.build:gradle:3.6.0-beta05")
    implementation(KOTLIN_STDLIB)

    testImplementation gradleTestKit()
    testImplementation(ANDROIDX_TEST_RUNNER)
    testImplementation(JUNIT)
    testImplementation(KOTLIN_TEST)
}

SdkResourceGenerator.generateForHostTest(project)

task buildOnServer(type: Copy) {
  from {
    def f = project.file("src/main/resources/scripts/lockClocks.sh")
    if (!f.exists()) {
        throw new GradleException(f.toString() + " does not exist")
    }
    return f
  }
  destinationDir BuildServerConfigurationKt.getDistributionDirectory(rootProject)
}

gradlePlugin {
    plugins {
        benchmark {
            id = "androidx.benchmark"
            implementationClass = "androidx.benchmark.gradle.BenchmarkPlugin"
        }
    }
}

androidx {
    name = "Android Benchmark Gradle Plugin"
    publish = Publish.SNAPSHOT_AND_RELEASE
    toolingProject = true
    mavenVersion = LibraryVersions.BENCHMARK
    mavenGroup = LibraryGroups.BENCHMARK
    inceptionYear = "2019"
    description = "Android Benchmark Gradle Plugin"
    compilationTarget = CompilationTarget.HOST
}
