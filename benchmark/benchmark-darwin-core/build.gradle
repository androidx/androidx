import androidx.build.Publish
import org.jetbrains.kotlin.gradle.plugin.mpp.BitcodeEmbeddingMode

plugins {
    id("AndroidXPlugin")
}

androidXMultiplatform {
    sourceSets {
        // We need a source set here even though the module is technically empty.
        // That is the only way we can have dependencies on the cinterop task work.
        // https://youtrack.jetbrains.com/issue/KT-40975
        iosArm64Main {
            dependsOn(commonMain)
        }
        iosSimulatorArm64Main {
            dependsOn(iosArm64Main)
        }
        iosX64Main {
            dependsOn(iosArm64Main)
        }
    }

    ios {
        compilations.main {
            cinterops {
                xcTestInterop {
                    defFile project.file("src/nativeInterop/cinterop/xcTestInterop.def")
                    // https://youtrack.jetbrains.com/issue/KT-48807#focus=Comments-27-5210791.0-0
                    compilerOpts("-DNS_FORMAT_ARGUMENT(A)=")
                }
            }
        }
        binaries.framework {
            baseName = "AndroidXBenchmarkXCTest"
            // https://youtrack.jetbrains.com/issue/KT-48552
            embedBitcode = BitcodeEmbeddingMode.DISABLE
        }
    }
}

androidx {
    name = "Benchmarks - Darwin Core"
    inceptionYear = "2022"
    description = "AndroidX Benchmarks - Darwin Core"
    publish = Publish.SNAPSHOT_ONLY
}
