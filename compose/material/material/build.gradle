/*./material/material/build.gradle
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.AndroidXComposePlugin
import androidx.build.LibraryType

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
    id("kotlinx-atomicfu")
    //id("AndroidXPaparazziPlugin") // isn't supported on Windows
}

AndroidXComposePlugin.applyAndConfigureKotlinPlugin(project)

dependencies {

    if(!AndroidXComposePlugin.isMultiplatformEnabled(project)) {
        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block below
         */
        api("androidx.compose.animation:animation-core:1.2.1")
        api(project(":compose:foundation:foundation"))
        api(project(":compose:material:material-icons-core"))
        api(project(":compose:material:material-ripple"))
        api("androidx.compose.runtime:runtime:1.2.1")
        api("androidx.compose.ui:ui:1.2.1")
        api("androidx.compose.ui:ui-text:1.2.1")

        implementation(libs.kotlinStdlibCommon)
        implementation("androidx.compose.animation:animation:1.2.1")
        implementation("androidx.compose.foundation:foundation-layout:1.2.1")
        implementation("androidx.compose.ui:ui-util:1.2.1")

        // TODO: remove next 3 dependencies when b/202810604 is fixed
        implementation("androidx.savedstate:savedstate:1.2.0")
        implementation("androidx.lifecycle:lifecycle-runtime:2.6.0")
        implementation("androidx.lifecycle:lifecycle-viewmodel:2.6.0")

        testImplementation(libs.testRules)
        testImplementation(libs.testRunner)
        testImplementation(libs.junit)
        testImplementation(libs.truth)

        androidTestImplementation(project(":compose:material:material:material-samples"))
        androidTestImplementation(project(":compose:test-utils"))
        androidTestImplementation(androidxArtifact(":test:screenshot:screenshot"))
        androidTestImplementation(libs.testRules)
        androidTestImplementation(libs.testRunner)
        androidTestImplementation(libs.junit)
        androidTestImplementation(libs.truth)
        androidTestImplementation(libs.dexmakerMockito)
        androidTestImplementation(libs.mockitoCore)
        androidTestImplementation(libs.mockitoKotlin)
        androidTestImplementation(libs.testUiautomator)

        lintPublish project(":compose:material:material-lint")

        samples(project(":compose:material:material:material-samples"))
    }
}

if(AndroidXComposePlugin.isMultiplatformEnabled(project)) {
    androidXComposeMultiplatform {
        android()
        desktop()
        darwin()
        js()
        wasm()
    }

    kotlin {

        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block above
         */
        sourceSets {
            commonMain.dependencies {
                implementation(libs.kotlinStdlibCommon)
                api(project(":compose:animation:animation-core"))
                api(project(":compose:foundation:foundation"))
                api(project(":compose:material:material-icons-core"))
                api(project(":compose:material:material-ripple"))
                api(project(":compose:runtime:runtime"))
                api(project(":compose:ui:ui"))
                api(project(":compose:ui:ui-text"))

                implementation(project(":compose:animation:animation"))
                implementation(project(":compose:foundation:foundation-layout"))
                implementation(project(":compose:ui:ui-util"))
                implementation(project(":annotation:annotation"))
            }

            androidMain.dependencies {
                api("androidx.annotation:annotation:1.1.0")

                // TODO: remove next 3 dependencies when b/202810604 is fixed
                implementation("androidx.savedstate:savedstate:1.2.0")
                implementation("androidx.lifecycle:lifecycle-runtime:2.6.0")
                implementation("androidx.lifecycle:lifecycle-viewmodel:2.6.0")
            }
            skikoMain {
                dependsOn(commonMain)
                dependencies {
                    implementation(libs.atomicFu)
                }
            }

            desktopMain.dependencies {
                implementation(libs.kotlinStdlib)
            }

            desktopMain.dependsOn(skikoMain)
            jsNativeMain.dependsOn(skikoMain)
            nativeMain.dependsOn(jsNativeMain)
            jsWasmMain.dependsOn(jsNativeMain)

            jsMain {
                dependsOn(jsWasmMain)
            }

            wasmJsMain {
                dependsOn(jsWasmMain)
            }

            // TODO(b/214407011): These dependencies leak into instrumented tests as well. If you
            //  need to add Robolectric (which must be kept out of androidAndroidTest), use a top
            //  level dependencies block instead:
            //  `dependencies { testImplementation(libs.robolectric) }`
            androidTest.dependencies {
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
            }

            androidAndroidTest.dependencies {
                implementation(project(":compose:material:material:material-samples"))
                implementation(project(":compose:test-utils"))
                implementation(androidxArtifact(":test:screenshot:screenshot"))

                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
                implementation(libs.dexmakerMockito)
                implementation(libs.mockitoCore)
                implementation(libs.mockitoKotlin)
                implementation(libs.testUiautomator)
            }

            commonTest.dependencies {
                implementation(kotlin("test"))
                implementation(project(":compose:ui:ui-test"))
            }

            desktopTest.dependencies {
                implementation(project(":compose:ui:ui-test-junit4"))
                implementation(libs.truth)
                implementation(libs.junit)
                implementation(libs.skikoCurrentOs)
            }

            configureEach {
                languageSettings.optIn("androidx.compose.material.ExperimentalMaterialApi")
            }
        }
    }
    dependencies {
        samples(project(":compose:material:material:material-samples"))
    }
}

// This task updates the translations of the localizable strings in this module.
// It obtains them from Android's base repository.
tasks.register("updateTranslations", UpdateTranslationsTask.class) {
    group = "localization"
    gitRepo = "https://github.com/androidx/androidx"
    repoResDirectories = ["compose/ui/ui/src/androidMain/res"]
    targetDirectory = project.file("src/skikoMain/kotlin/androidx/compose/material/l10n")
    targetPackageName = "androidx.compose.material.l10n"
    kotlinStringsPackageName = "androidx.compose.material"
    stringByResourceName = [
            "navigation_menu": "NavigationMenu",
            "close_drawer": "CloseDrawer",
            "close_sheet": "CloseSheet",
            "default_error_message": "DefaultErrorMessage",
            "dropdown_menu": "ExposedDropdownMenu",
            "range_start": "SliderRangeStart",
            "range_end": "SliderRangeEnd",
    ]
    // This is all the locales translated by Compose on Android in the ui module:
    // https://github.com/androidx/androidx/tree/androidx-main/compose/ui/ui/src/androidMain/res
    // with the exception of
    // - b+sr+Latn which doesn't appear to be supported by Java
    // - en_XC which has weird invisible LRM characters, and the visible text is the same as for
    //   en anyway.
    locales = [
            "en", "af", "am", "ar", "as", "az", "be", "bg", "bn", "bs", "ca", "cs", "da", "de",
            "el", "en_AU", "en_CA", "en_GB", "en_IN", "es", "es_US", "et", "eu", "fa",
            "fi", "fr", "fr_CA", "gl", "gu", "hi", "hr", "hu", "hy", "in", "is", "it", "iw",
            "ja", "ka", "kk", "km", "kn", "ko", "ky", "lo", "lt", "lv", "mk", "ml", "mn", "mr",
            "ms", "my", "nb", "ne", "nl", "or", "pa", "pl", "pt", "pt_BR", "pt_PT", "ro", "ru",
            "si", "sk", "sl", "sq", "sr", "sv", "sw", "ta", "te", "th", "tl", "tr", "uk", "ur",
            "uz", "vi", "zh_CN", "zh_HK", "zh_TW", "zu"
    ]
}

androidx {
    name = "Compose Material Components"
    type = LibraryType.PUBLISHED_LIBRARY
    inceptionYear = "2018"
    description = "Compose Material Design Components library"
    legacyDisableKotlinStrictApiMode = true
}

// Screenshot tests related setup
android {
    sourceSets.androidTest.assets.srcDirs +=
            project.rootDir.absolutePath + "/golden/compose/material/material"
    namespace "androidx.compose.material"
}
