/*
 * Copyright 2024 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.AndroidXComposePlugin
import androidx.build.JetbrainsAndroidXPlugin
import androidx.build.LibraryType
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
    id("JetbrainsAndroidXPlugin")
}

AndroidXComposePlugin.applyAndConfigureKotlinPlugin(project)
JetbrainsAndroidXPlugin.applyAndConfigure(project)

androidXComposeMultiplatform {
    android()
    desktop()
    darwin()
    js()
    wasm()
}

kotlin {
    sourceSets {
        commonMain {
            dependencies {
                implementation(project(":navigation:navigation-compose"))
                implementation(project(":compose:material:material"))
                implementation(libs.kotlinStdlib)
            }
        }

        androidInstrumentedTest {
            dependencies {
                implementation(project(":compose:test-utils"))
                implementation(project(":navigation:navigation-testing"))
                implementation(project(":compose:ui:ui-test-junit4"))
                implementation(project(":compose:ui:ui-test-manifest"))
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
                implementation(libs.testRules)
            }
       }

        jbMain.dependsOn(commonMain)
        desktopMain.dependsOn(jbMain)
        nativeMain.dependsOn(jbMain)
        webMain.dependsOn(jbMain)

        targets.all { target ->
            if (target.platformType == KotlinPlatformType.native) {
                target.compilations["main"].defaultSourceSet {
                    dependsOn(nativeMain)
                }
            } else if (target.platformType in [
                    KotlinPlatformType.js,
                    KotlinPlatformType.wasm
            ]) {
                target.compilations["main"].defaultSourceSet {
                    dependsOn(webMain)
                }
            }
        }
    }
}

androidx {
    name = "Compose Material Navigation"
    type = LibraryType.PUBLISHED_LIBRARY
    mavenVersion = LibraryVersions.COMPOSE
    inceptionYear = "2024"
    description = "Compose Material integration with Navigation"
//    samples(projectOrArtifact(":compose:material:material-navigation-samples"))
}

android {
    namespace "androidx.compose.material.navigation"
}
