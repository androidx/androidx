/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.KmpPlatformsKt
import androidx.build.LibraryType

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

def desktopEnabled = KmpPlatformsKt.enableDesktop(project)

androidXMultiplatform {
    android()
    if (desktopEnabled) desktop()

    sourceSets {
        commonMain {
            dependencies {
                api(project(":compose:ui:ui-test"))
                implementation(libs.kotlinStdlib)
                implementation(libs.kotlinCoroutinesCore)
                implementation(libs.kotlinCoroutinesTest)
            }
        }

        commonTest {
            dependencies {
            }
        }

        jvmMain {
            dependsOn(commonMain)
            dependencies {
                api(libs.junit)
                api(libs.kotlinStdlib)
                api(libs.kotlinStdlibCommon)

                compileOnly("androidx.annotation:annotation:1.1.0")
            }
        }


        androidMain {
            dependsOn(jvmMain)
            dependencies {
                api("androidx.activity:activity:1.2.1")
                implementation "androidx.activity:activity-compose:1.3.0"
                api(libs.testExtJunit)
                implementation("androidx.annotation:annotation:1.1.0")

                implementation(project(":compose:runtime:runtime-saveable"))
                implementation("androidx.lifecycle:lifecycle-common:2.5.1")
                implementation("androidx.lifecycle:lifecycle-runtime:2.5.1")
                implementation("androidx.test:core:1.5.0")
                implementation(libs.testMonitor)
                implementation("androidx.test.espresso:espresso-core:3.3.0")
                implementation("androidx.test.espresso:espresso-idling-resource:3.5.0")
            }
        }

        if (desktopEnabled) {
            desktopMain {
                dependsOn(jvmMain)
                dependencies {
                    implementation(libs.truth)
                    implementation(libs.skiko)
                }
            }
        }

        jvmTest {
            dependencies {
            }
        }

        androidAndroidTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(project(":compose:animation:animation"))
                implementation(project(":compose:test-utils"))
                implementation(project(":compose:material:material"))
                implementation("androidx.fragment:fragment-testing:1.4.1")
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.truth)
                implementation(libs.mockitoCore)
                implementation(libs.dexmakerMockito)
                implementation(libs.mockitoKotlin)
            }
        }

        // TODO(b/214407011): These dependencies leak into instrumented tests as well. If you
        //  need to add Robolectric (which must be kept out of androidAndroidTest), use a top
        //  level dependencies block instead:
        //  `dependencies { testImplementation(libs.robolectric) }`
        androidTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(project(":compose:animation:animation-core"))
                implementation(project(":compose:material:material"))
                implementation(project(":compose:test-utils"))
                implementation(libs.truth)
            }
        }

        if (desktopEnabled) {
            desktopTest {
                dependsOn(jvmTest)
                dependencies {
                    implementation(libs.truth)
                    implementation(libs.junit)
                    implementation(libs.kotlinTest)
                    implementation(libs.skikoCurrentOs)
                    implementation(project(":compose:foundation:foundation"))
                    implementation(project(":compose:ui:ui-test-junit4"))
                }
            }
        }
    }
}


android {
    lintOptions {
        disable("InvalidPackage")
    }
    namespace "androidx.compose.ui.test.junit4"
}

dependencies {
    // Can't declare this in kotlin { sourceSets { androidTest.dependencies { .. } } } as that
    // leaks into instrumented tests (b/214407011)
    testImplementation(libs.robolectric)
}

androidx {
    name = "Compose Testing for JUnit4"
    type = LibraryType.PUBLISHED_TEST_LIBRARY
    inceptionYear = "2020"
    description = "Compose testing integration with JUnit4"
    legacyDisableKotlinStrictApiMode = true
}
