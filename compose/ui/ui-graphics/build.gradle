/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */
import androidx.build.LibraryType
import androidx.build.PlatformIdentifier

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

androidXMultiplatform {
    android()
    desktop()

    defaultPlatform(PlatformIdentifier.ANDROID)

    sourceSets {
        commonMain {
            dependencies {
                implementation(libs.kotlinStdlibCommon)
                api(libs.androidx.annotation)

                api(project(":compose:ui:ui-unit"))
                implementation("androidx.compose.runtime:runtime:1.6.0")
                implementation(project(":compose:ui:ui-util"))
                implementation("androidx.collection:collection:1.4.0")
            }
        }

        commonTest {
            dependencies {
                implementation(kotlin("test"))
            }
        }

        skikoMain {
            dependsOn(commonMain)
            dependencies {
                api(libs.skikoCommon)
            }
        }

        jvmMain {
            dependsOn(commonMain)
            dependencies {
            }
        }

        androidMain {
            dependsOn(jvmMain)
            dependencies {
                implementation("androidx.graphics:graphics-path:1.0.0-beta02")
                api("androidx.annotation:annotation-experimental:1.4.0")
            }
        }

        desktopMain {
            dependsOn(jvmMain)
            dependsOn(skikoMain)
            dependencies {
                implementation(libs.kotlinStdlib)
                implementation(libs.kotlinStdlibJdk8)
            }
        }

        jvmTest {
            dependencies {
            }
        }

        androidInstrumentedTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(project(":compose:ui:ui-graphics:ui-graphics-samples"))
                implementation(project(":compose:ui:ui-test-junit4"))
                implementation(project(":compose:test-utils"))
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.espressoCore)
                implementation(libs.junit)
                implementation(libs.truth)
            }
        }

        androidUnitTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
            }
        }

        desktopTest {
            resources.srcDirs += "src/desktopTest/res"
            dependsOn(jvmTest)
            dependencies {
                implementation(project(":compose:ui:ui-test-junit4"))
                implementation(libs.junit)
                implementation(libs.truth)
                implementation(libs.skikoCurrentOs)
            }
        }
    }
}

dependencies {
    lintPublish(project(":compose:ui:ui-graphics-lint"))
}

androidx {
    name = "Compose Graphics"
    type = LibraryType.PUBLISHED_LIBRARY
    inceptionYear = "2020"
    description = "Compose graphics"
    legacyDisableKotlinStrictApiMode = true
}

androidxCompose {
    composeCompilerPluginEnabled = false
}

android {
    namespace "androidx.compose.ui.graphics"
}

tasks.findByName("desktopTest").configure {
    systemProperties["GOLDEN_PATH"] = project.rootDir.absolutePath + "/../../golden"
}
