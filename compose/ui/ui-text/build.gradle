/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */


import androidx.build.KotlinTarget
import androidx.build.LibraryType
import androidx.build.PlatformIdentifier

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

androidXMultiplatform {
    android()
    jvmStubs()
    linuxX64Stubs()

    defaultPlatform(PlatformIdentifier.ANDROID)

    sourceSets {
        commonMain {
            dependencies {
                implementation(libs.kotlinStdlib)
                implementation(libs.kotlinCoroutinesCore)

                api(project(":compose:ui:ui-graphics"))
                api(project(":compose:ui:ui-unit"))

                // when updating the runtime version please also update the runtime-saveable version
                implementation(project(":compose:runtime:runtime"))
                implementation(project(":compose:runtime:runtime-saveable"))

                implementation(project(":compose:ui:ui-util"))

                // TODO: Pin androidx.collection when SieveCache is available
                // implementation("androidx.collection:collection:1.4.2")
                implementation(project(":collection:collection"))
            }
        }

        commonTest {
            dependencies {
            }
        }

        jvmMain {
            dependsOn(commonMain)
            dependencies {
            }
        }

        androidMain {
            dependsOn(commonMain)
            dependsOn(jvmMain)
            dependencies {
                api("androidx.annotation:annotation:1.8.1")
                api("androidx.annotation:annotation-experimental:1.4.1")
                implementation("androidx.core:core:1.7.0")
                implementation("androidx.emoji2:emoji2:1.4.0")
            }
        }

        commonStubsMain {
            dependsOn(commonMain)
        }

        jvmStubsMain {
            dependsOn(commonStubsMain)
        }

        linuxx64StubsMain {
            dependsOn(commonStubsMain)
        }

        androidInstrumentedTest {
            dependsOn(commonTest)
            dependencies {
                implementation("androidx.emoji2:emoji2-bundled:1.4.0")
                implementation(project(":compose:ui:ui-test-junit4"))
                implementation(project(":internal-testutils-fonts"))
                implementation(project(":compose:foundation:foundation"))
                implementation(project(":compose:test-utils"))
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.espressoCore)
                implementation(libs.junit)
                implementation(libs.dexmakerMockito)
                implementation(libs.mockitoCore)
                implementation(libs.truth)
                implementation(libs.mockitoKotlin)
            }
        }

        androidUnitTest {
            dependsOn(commonTest)
            dependencies {
                implementation(project(":internal-testutils-fonts"))
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
                implementation(libs.kotlinReflect)
                implementation(libs.kotlinTest)
                implementation(libs.mockitoCore4)
                implementation(libs.mockitoKotlin4)
                implementation(libs.byteBuddy)
            }
        }
    }
}

dependencies {
    lintPublish(project(":compose:ui:ui-text-lint"))
    lintChecks(project(":compose:ui:ui-text-lint"))
}

androidx {
    name = "Compose UI Text"
    type = LibraryType.PUBLISHED_LIBRARY_ONLY_USED_BY_KOTLIN_CONSUMERS
    inceptionYear = "2019"
    description = "Compose Text primitives and utilities"
    legacyDisableKotlinStrictApiMode = true
    metalavaK2UastEnabled = false
    samples(project(":compose:ui:ui-text:ui-text-samples"))
    kotlinTarget = KotlinTarget.KOTLIN_1_9
}

android {
    compileSdk 35
    namespace "androidx.compose.ui.text"
    // TODO(b/328001575)
    experimentalProperties["android.lint.useK2Uast"] = false
    buildTypes.configureEach {
        consumerProguardFiles("proguard-rules.pro")
    }
}
