/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.AndroidXComposePlugin
import androidx.build.LibraryGroups
import androidx.build.LibraryType

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

AndroidXComposePlugin.applyAndConfigureKotlinPlugin(project)

android {
    if (!AndroidXComposePlugin.isMultiplatformEnabled(project)) {
        sourceSets {
            test.java.srcDirs += "src/androidCommonTest/kotlin"
            androidTest.java.srcDirs += "src/androidCommonTest/kotlin"
        }
    }

    lintOptions {
        disable("InvalidPackage")
    }
}

dependencies {
    kotlinPlugin(project(":compose:compiler:compiler"))

    if (!AndroidXComposePlugin.isMultiplatformEnabled(project)) {
        api(project(":compose:runtime:runtime"))
        api(project(":compose:ui:ui"))
        api(project(":compose:ui:ui-graphics"))
        api(project(":compose:ui:ui-text"))
        api(project(":compose:ui:ui-unit"))
        api(libs.kotlinCoroutinesCore)
        api(libs.kotlinCoroutinesTest)
        api(libs.kotlinStdlib)
        api(libs.kotlinStdlibCommon)

        implementation(project(":compose:ui:ui-util"))
        implementation("androidx.annotation:annotation:1.1.0")
        implementation(libs.espressoCore)
        implementation(libs.testMonitor)

        testImplementation(project(":compose:test-utils"))
        testImplementation(libs.truth)
        testImplementation(libs.robolectric)
        testImplementation(libs.mockitoCore)
        testImplementation(libs.mockitoKotlin)

        androidTestImplementation("androidx.activity:activity-compose:1.3.1")
        androidTestImplementation(project(":compose:material:material"))
        androidTestImplementation(project(":compose:test-utils"))
        androidTestImplementation(project(":compose:ui:ui-test-junit4"))
        androidTestImplementation(libs.truth)
        androidTestImplementation(libs.mockitoCore)
        androidTestImplementation(libs.dexmakerMockito)
        androidTestImplementation(libs.mockitoKotlin)
    }
}


if (AndroidXComposePlugin.isMultiplatformEnabled(project)) {
    kotlin {
        android()
        jvm("desktop")

        sourceSets {
            commonMain.dependencies {
                api(project(":compose:ui:ui"))
                api(project(":compose:ui:ui-text"))
                api(project(":compose:ui:ui-unit"))
                api(libs.kotlinStdlib)

                implementation(project(":compose:ui:ui-util"))
            }

            jvmMain.dependencies {
                api(project(":compose:runtime:runtime"))
                api(libs.kotlinCoroutinesCore)
                api(libs.kotlinCoroutinesTest)
                api(libs.kotlinStdlibCommon)
            }

            androidMain.dependencies {
                api(project(":compose:ui:ui-graphics"))

                implementation("androidx.annotation:annotation:1.1.0")
                implementation(libs.espressoCore)
                implementation(libs.testMonitor)
            }

            androidCommonTest.dependencies {
                implementation(project(":compose:test-utils"))
                implementation(libs.truth)
                implementation(libs.mockitoCore)
                implementation(libs.mockitoKotlin)
            }

            androidTest.dependencies {
                implementation(libs.robolectric)
            }

            androidAndroidTest.dependencies {
                implementation(project(":compose:material:material"))
                implementation(project(":compose:ui:ui-test-junit4"))
                implementation("androidx.activity:activity-compose:1.3.1")
                implementation(libs.dexmakerMockito)
            }

            desktopMain.dependencies {
                implementation(libs.junit)
                implementation(libs.truth)
                implementation(libs.skiko)
            }

            androidMain.dependsOn(jvmMain)
            desktopMain.dependsOn(jvmMain)
            jvmMain.dependsOn(commonMain)

            androidCommonTest.dependsOn(commonTest)
            androidTest.dependsOn(androidCommonTest)
            androidAndroidTest.dependsOn(androidCommonTest)
        }
    }
}

androidx {
    name = "Compose Testing"
    type = LibraryType.PUBLISHED_TEST_LIBRARY
    mavenGroup = LibraryGroups.Compose.UI
    inceptionYear = "2019"
    description = "Compose testing library"
    legacyDisableKotlinStrictApiMode = true
}
