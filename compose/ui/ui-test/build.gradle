/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.KmpPlatformsKt
import androidx.build.LibraryType

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

def desktopEnabled = KmpPlatformsKt.enableDesktop(project)

androidXMultiplatform {
    android()
    if (desktopEnabled) desktop()

    sourceSets {
        commonMain {
            dependencies {
                api(project(":compose:ui:ui"))
                api(project(":compose:ui:ui-text"))
                api(project(":compose:ui:ui-unit"))
                api(project(":compose:runtime:runtime"))
                api(libs.kotlinStdlib)

                implementation(project(":compose:ui:ui-util"))
            }
        }

        commonTest {
            dependencies {
            }
        }

        jvmMain {
            dependsOn(commonMain)
            dependencies {
                api(libs.kotlinCoroutinesCore)
                api(libs.kotlinCoroutinesTest)
                api(libs.kotlinStdlibCommon)
            }
        }


        androidMain {
            dependsOn(jvmMain)
            dependencies {
                api(project(":compose:ui:ui-graphics"))

                implementation("androidx.annotation:annotation:1.1.0")
                implementation("androidx.core:core-ktx:1.2.0")
                implementation("androidx.test.espresso:espresso-core:3.5.0")
                implementation(libs.testMonitor)
            }
        }

        if (desktopEnabled) {
            desktopMain {
                dependsOn(jvmMain)
                dependencies {
                    implementation(libs.junit)
                    implementation(libs.truth)
                    implementation(libs.skiko)
                }
            }
        }

        jvmTest {
            dependencies {
            }
        }

        androidCommonTest {
            dependsOn(commonTest)
            dependencies {
                implementation(project(":compose:test-utils"))
                implementation(libs.truth)
            }
        }

        androidAndroidTest {
            dependsOn(jvmTest)
            dependsOn(androidCommonTest)
            dependencies {
                implementation(project(":compose:material:material"))
                implementation(project(":compose:ui:ui-test-junit4"))
                implementation("androidx.activity:activity-compose:1.3.1")
                implementation(libs.mockitoCore)
                implementation(libs.mockitoKotlin)
                implementation(libs.dexmakerMockito)
                implementation(libs.kotlinTest)
            }
        }

        // TODO(b/214407011): These dependencies leak into instrumented tests as well. If you
        //  need to add Robolectric (which must be kept out of androidAndroidTest), use a top
        //  level dependencies block instead:
        //  `dependencies { testImplementation(libs.robolectric) }`
        androidTest {
            dependsOn(jvmTest)
            dependsOn(androidCommonTest)
            dependencies {
                implementation(libs.mockitoCore4)
                implementation(libs.mockitoKotlin4)
            }
        }

        if (desktopEnabled) {
            desktopTest {
                dependsOn(jvmTest)
            }
        }
    }
}

android {
    sourceSets {
        test.java.srcDirs += "src/androidCommonTest/kotlin"
        androidTest.java.srcDirs += "src/androidCommonTest/kotlin"
    }

    lintOptions {
        disable("InvalidPackage")
    }
    namespace "androidx.compose.ui.test"
}

dependencies {
    // Can't declare this in kotlin { sourceSets { androidTest.dependencies { .. } } } as that
    // leaks into instrumented tests (b/214407011)
    testImplementation(libs.robolectric)
}

androidx {
    name = "Compose Testing"
    type = LibraryType.PUBLISHED_TEST_LIBRARY
    inceptionYear = "2019"
    description = "Compose testing library"
    legacyDisableKotlinStrictApiMode = true
}
