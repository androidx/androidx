/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */
import androidx.build.LibraryType
import androidx.build.PlatformIdentifier

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

androidXMultiplatform {
    android()
    desktop()

    defaultPlatform(PlatformIdentifier.ANDROID)

    sourceSets {
        commonMain {
            dependencies {
                api(project(":compose:ui:ui"))
                api(project(":compose:ui:ui-text"))
                api(project(":compose:ui:ui-unit"))
                api(project(":compose:runtime:runtime"))
                api(libs.kotlinStdlib)

                implementation(project(":compose:ui:ui-util"))
            }
        }

        commonTest {
            dependencies {
            }
        }

        jvmMain {
            dependsOn(commonMain)
            dependencies {
                api(libs.kotlinCoroutinesCore)
                api(libs.kotlinCoroutinesTest)
                api(libs.kotlinStdlibCommon)
            }
        }


        androidMain {
            dependsOn(jvmMain)
            dependencies {
                implementation "androidx.activity:activity-compose:1.3.0"

                api(project(":compose:ui:ui-graphics"))

                implementation("androidx.annotation:annotation:1.1.0")
                implementation("androidx.core:core-ktx:1.12.0")
                implementation("androidx.test.espresso:espresso-core:3.5.0")
                implementation("androidx.test.espresso:espresso-idling-resource:3.5.0")
            	implementation("androidx.test:monitor:1.6.1")
	        }
        }

        desktopMain {
            dependsOn(jvmMain)
            dependencies {
                implementation(libs.junit)
                implementation(libs.truth)
                implementation(libs.skiko)
            }
        }

        jvmTest {
            dependencies {
            }
        }

        androidCommonTest {
            dependsOn(commonTest)
            dependencies {
                implementation(project(":compose:test-utils"))
                implementation(libs.truth)
            }
        }

        androidInstrumentedTest {
            dependsOn(jvmTest)
            dependsOn(androidCommonTest)
            dependencies {
                implementation(project(":compose:material:material"))
                implementation(project(":compose:animation:animation"))
                implementation(project(":compose:ui:ui-test"))
                implementation("androidx.activity:activity-compose:1.3.1")
                implementation("androidx.fragment:fragment-testing:1.4.1")

                implementation(libs.mockitoCore)
                implementation(libs.mockitoKotlin)
                implementation(libs.dexmakerMockito)
                implementation(libs.kotlinTest)
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.truth)

            }
        }

        androidUnitTest {
            dependsOn(jvmTest)
            dependsOn(androidCommonTest)
            dependencies {
                implementation(libs.truth)
                implementation(libs.robolectric)
                implementation(libs.mockitoCore4)
                implementation(libs.mockitoKotlin4)

                implementation(project(":compose:animation:animation-core"))
                implementation(project(":compose:material:material"))
                implementation(project(":compose:test-utils"))
            }
        }

        desktopTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(libs.truth)
                implementation(libs.junit)
                implementation(libs.kotlinTest)
                implementation(libs.skikoCurrentOs)
                implementation(project(":compose:foundation:foundation"))
                implementation(project(":compose:ui:ui-test"))
            }
        }
    }
}

android {
    sourceSets {
        test.java.srcDirs += "src/androidCommonTest/kotlin"
        androidTest.java.srcDirs += "src/androidCommonTest/kotlin"
    }

    lintOptions {
        disable("InvalidPackage")
    }
    namespace "androidx.compose.ui.test"
}

androidx {
    name = "Compose Testing"
    type = LibraryType.PUBLISHED_TEST_LIBRARY
    inceptionYear = "2019"
    description = "Compose testing library"
    legacyDisableKotlinStrictApiMode = true
}
