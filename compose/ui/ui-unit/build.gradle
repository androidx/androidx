/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.KmpPlatformsKt
import androidx.build.LibraryType
import androidx.build.PlatformIdentifier

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

def desktopEnabled = KmpPlatformsKt.enableDesktop(project)

androidXMultiplatform {
    android()
    if (desktopEnabled) desktop()

    defaultPlatform(PlatformIdentifier.ANDROID)

    sourceSets {
        commonMain {
            dependencies {
                implementation(libs.kotlinStdlibCommon)
                api(project(":compose:ui:ui-geometry"))

                implementation(project(":compose:runtime:runtime"))
                implementation(project(":compose:ui:ui-util"))
                api("androidx.annotation:annotation:1.1.0")
            }
        }

        commonTest {
            dependencies {
                implementation(kotlin("test-junit"))
            }
        }

        jvmMain {
            dependsOn(commonMain)
            dependencies {
                implementation(libs.kotlinStdlib)
            }
        }


        androidMain {
            dependsOn(jvmMain)
            dependencies {
                api("androidx.annotation:annotation:1.1.0")
                implementation('androidx.collection:collection-ktx:1.2.0')
            }
        }

        if (desktopEnabled) {
            desktopMain {
                dependsOn(jvmMain)
                dependencies {
                    implementation(project(":compose:runtime:runtime"))
                }
            }
        }

        jvmTest {
            dependencies {
            }
        }

        androidInstrumentedTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.testExtJunit)
                implementation(libs.espressoCore)
                implementation(libs.truth)
                implementation('androidx.collection:collection-ktx:1.2.0')
            }
        }

        androidUnitTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(libs.truth)
                implementation('androidx.collection:collection-ktx:1.2.0')
            }
        }

        if (desktopEnabled) {
            desktopTest {
                dependsOn(jvmTest)
            }
        }
    }
}

dependencies {
    samples(projectOrArtifact(":compose:ui:ui-unit:ui-unit-samples"))
}

androidx {
    name = "Compose Unit"
    type = LibraryType.PUBLISHED_LIBRARY
    inceptionYear = "2020"
    description = "Compose classes for simple units"
    legacyDisableKotlinStrictApiMode = true
}

android {
    namespace "androidx.compose.ui.unit"
}
