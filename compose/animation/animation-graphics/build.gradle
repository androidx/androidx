/*
 * Copyright 2021 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import androidx.build.KmpPlatformsKt
import androidx.build.LibraryType
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

def desktopEnabled = KmpPlatformsKt.enableDesktop(project)

androidXMultiplatform {
    android()
    if (desktopEnabled) desktop()

    sourceSets {
        commonMain {
            dependencies {
                implementation(libs.kotlinStdlibCommon)

                api(project(":compose:animation:animation"))
                api(project(":compose:foundation:foundation-layout"))
                api(project(":compose:runtime:runtime"))
                api(project(":compose:ui:ui"))
                api(project(":compose:ui:ui-geometry"))

                implementation(project(":compose:ui:ui-util"))
            }
        }
        androidMain.dependencies {
            api("androidx.annotation:annotation:1.1.0")
            implementation("androidx.core:core-ktx:1.5.0")
        }

        commonTest {
            dependencies {
            }
        }

        jvmMain {
            dependsOn(commonMain)
            dependencies {
            }
        }


        androidMain {
            dependsOn(jvmMain)
            dependencies {
            }
        }

        if (desktopEnabled) {
            desktopMain {
                dependsOn(jvmMain)
                dependencies {
                    implementation(libs.kotlinStdlib)
                    api(project(":compose:foundation:foundation-layout"))
                    api(project(":compose:runtime:runtime"))
                    api(project(":compose:ui:ui"))
                    api(project(":compose:ui:ui-geometry"))
                    implementation(project(":compose:ui:ui-util"))
                }
            }
        }

        jvmTest {
            dependsOn(commonTest)
            dependencies {
            }
        }

        androidAndroidTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
                implementation("androidx.compose.foundation:foundation:1.2.1")
                implementation("androidx.compose.ui:ui-test-junit4:1.2.1")
                implementation(project(":compose:test-utils"))
            }
        }

        // TODO(b/214407011): These dependencies leak into instrumented tests as well. If you
        //  need to add Robolectric (which must be kept out of androidAndroidTest), use a top
        //  level dependencies block instead:
        //  `dependencies { testImplementation(libs.robolectric) }`
        androidTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
            }
        }

        if (desktopEnabled) {
            desktopTest {
                dependsOn(jvmTest)
                dependsOn(desktopMain)
                dependencies {
                }
            }
        }
    }
}

androidx {
    name = "Compose Animation Graphics"
    type = LibraryType.PUBLISHED_LIBRARY
    inceptionYear = "2021"
    description = "Compose Animation Graphics Library for using animated-vector resources in Compose"
}

android {
    namespace "androidx.compose.animation.graphics"
}
