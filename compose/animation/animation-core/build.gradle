/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import androidx.build.KmpPlatformsKt
import androidx.build.LibraryType
import androidx.build.PlatformIdentifier

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

def desktopEnabled = KmpPlatformsKt.enableDesktop(project)

androidXMultiplatform {
    android()
    if (desktopEnabled) desktop()

    defaultPlatform(PlatformIdentifier.ANDROID)

    sourceSets {
        commonMain {
            dependencies {
                implementation(project(":compose:runtime:runtime"))
                implementation(project(":compose:ui:ui"))
                implementation(project(":compose:ui:ui-unit"))
                implementation(project(":compose:ui:ui-util"))
                implementation(libs.kotlinStdlibCommon)
                api(libs.kotlinCoroutinesCore)
            }
        }

        commonTest {
            dependencies {
            }
        }

        jvmMain {
            dependsOn(commonMain)
            dependencies {
                implementation(libs.kotlinStdlib)
                api(libs.kotlinCoroutinesCore)
            }
        }


        androidMain {
            dependsOn(jvmMain)
            dependencies {
                api("androidx.annotation:annotation:1.1.0")
                implementation(libs.kotlinStdlib)
            }
        }

        if (desktopEnabled) {
            desktopMain {
                dependsOn(jvmMain)
                dependencies {
                    implementation(libs.kotlinStdlib)
                    implementation(project(":compose:runtime:runtime"))
                    implementation(project(":compose:ui:ui"))
                    implementation(project(":compose:ui:ui-unit"))
                    implementation(project(":compose:ui:ui-util"))
                }
            }
        }

        jvmTest {
            dependencies {
            }
        }

        androidInstrumentedTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.testCore)
                implementation(libs.junit)
                implementation(libs.truth)
                implementation(project(":compose:animation:animation"))
                implementation("androidx.compose.ui:ui-test-junit4:1.2.1")
                implementation(project(":compose:test-utils"))
            }
        }

        androidUnitTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
                implementation(libs.kotlinCoroutinesCore)
            }
        }

        if (desktopEnabled) {
            desktopTest {
                dependsOn(jvmTest)
            }
        }
    }
}

dependencies {
    lintPublish project(":compose:animation:animation-core-lint")
}

androidx {
    name = "Compose Animation Core"
    type = LibraryType.PUBLISHED_LIBRARY
    inceptionYear = "2019"
    description = "Animation engine and animation primitives that are the building blocks of the Compose animation library"
    legacyDisableKotlinStrictApiMode = true
}

android {
    namespace "androidx.compose.animation.core"
}
