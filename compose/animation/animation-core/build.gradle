/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import androidx.build.AndroidXComposePlugin
import androidx.build.LibraryGroups
import androidx.build.LibraryType
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

AndroidXComposePlugin.applyAndConfigureKotlinPlugin(project)

dependencies {
    kotlinPlugin(project(":compose:compiler:compiler"))

    if (!AndroidXComposePlugin.isMultiplatformEnabled(project)) {
        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block below
         */

        api("androidx.annotation:annotation:1.1.0")

        implementation("androidx.compose.runtime:runtime:1.1.0-rc01")
        implementation("androidx.compose.ui:ui:1.0.0")
        implementation("androidx.compose.ui:ui-unit:1.0.0")
        implementation("androidx.compose.ui:ui-util:1.0.0")
        implementation(libs.kotlinStdlib)
        api(libs.kotlinCoroutinesCore)

        testImplementation(libs.testRules)
        testImplementation(libs.testRunner)
        testImplementation(libs.junit)
        testImplementation(libs.truth)
        testImplementation(libs.kotlinCoroutinesCore)

        androidTestImplementation(libs.testRules)
        androidTestImplementation(libs.testRunner)
        androidTestImplementation(libs.testCore)
        androidTestImplementation(libs.junit)
        androidTestImplementation(project(":compose:animation:animation"))
        androidTestImplementation("androidx.compose.ui:ui-test-junit4:1.0.0")
        androidTestImplementation(project(":compose:test-utils"))

        lintPublish project(":compose:animation:animation-core-lint")

        samples(project(":compose:animation:animation-core:animation-core-samples"))
    }
}

if (AndroidXComposePlugin.isMultiplatformEnabled(project)) {
    androidXComposeMultiplatform {
        android()
        desktop()
    }

    kotlin {
        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block above
         */
        sourceSets {

            jvmMain {
                dependencies {
                    implementation(libs.kotlinStdlib)
                    api(libs.kotlinCoroutinesCore)
                }
            }

            commonMain.dependencies {
                implementation(project(":compose:runtime:runtime"))
                implementation(project(":compose:ui:ui"))
                implementation(project(":compose:ui:ui-unit"))
                implementation(project(":compose:ui:ui-util"))
                implementation(libs.kotlinStdlibCommon)
                api(libs.kotlinCoroutinesCore)
            }

            androidMain {
                dependencies {
                    api("androidx.annotation:annotation:1.1.0")
                    implementation(libs.kotlinStdlib)
                }
            }
            desktopMain {
                dependencies {
                    implementation(libs.kotlinStdlib)
                }
            }

            androidTest.dependencies {
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
                implementation(libs.kotlinCoroutinesCore)
            }

            androidAndroidTest.dependencies {
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.testCore)
                implementation(libs.junit)
                implementation(project(":compose:animation:animation"))
                implementation(project(":compose:ui:ui-test-junit4"))
                implementation(project(":compose:test-utils"))
            }
        }
    }
    dependencies {
        samples(project(":compose:animation:animation-core:animation-core-samples"))
    }
}

androidx {
    name = "Compose Animation Core"
    type = LibraryType.PUBLISHED_LIBRARY
    mavenGroup = LibraryGroups.Compose.ANIMATION
    inceptionYear = "2019"
    description = "Animation engine and animation primitives that are the building blocks of the Compose animation library"
    legacyDisableKotlinStrictApiMode = true
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += [
                "-Xopt-in=kotlin.Experimental",
        ]
    }
}
