/*
 * Copyright (C) 2021 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */
import androidx.build.LibraryType
import androidx.build.PlatformIdentifier

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

androidXMultiplatform {
    android()
    jvmStubs()

    defaultPlatform(PlatformIdentifier.ANDROID)

    sourceSets {
        commonMain {
            dependencies {
                implementation(libs.kotlinStdlib)
                // Keep pinned unless there is a need for tip of tree behavior
                implementation("androidx.collection:collection:1.4.0")
                implementation("androidx.compose.animation:animation-core:1.6.0")
                implementation("androidx.compose.ui:ui-util:1.6.0")
                api("androidx.compose.foundation:foundation:1.7.0-beta02")
                api("androidx.compose.foundation:foundation-layout:1.7.0-beta02")
                api("androidx.compose.material:material-icons-core:1.6.0")
                api("androidx.compose.material:material-ripple:1.7.0-beta02")
                api("androidx.compose.runtime:runtime:1.7.0-beta02")
                api("androidx.compose.ui:ui:1.6.0")
                api("androidx.compose.ui:ui-text:1.6.0")
                api("androidx.graphics:graphics-shapes:1.0.0-beta01")
            }
        }

        commonTest {
            dependencies {
            }
        }

        jvmMain {
            dependsOn(commonMain)
            dependencies {
            }
        }

        androidMain {
            dependsOn(jvmMain)
            dependencies {
                api("androidx.annotation:annotation:1.1.0")
                api("androidx.annotation:annotation-experimental:1.4.1")
                implementation("androidx.activity:activity-compose:1.8.2")

                implementation("androidx.lifecycle:lifecycle-common-java8:2.6.1")
            }
        }

        jvmStubsMain {
            dependsOn(jvmMain)
            dependencies {
            }
        }

        androidUnitTest {
            dependsOn(commonTest)
            dependencies {
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
            }
        }

        androidInstrumentedTest {
            dependsOn(commonTest)
            dependencies {
                implementation(project(":compose:material3:material3:material3-samples"))
                implementation(project(":compose:test-utils"))
                implementation(project(':compose:foundation:foundation-layout'))
                implementation(project(":test:screenshot:screenshot"))
                implementation(projectOrArtifact(":core:core"))
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
                implementation(libs.dexmakerMockitoInlineExtended)
                implementation(libs.mockitoKotlin)
                implementation(libs.testUiautomator)
            }
        }
    }
}

dependencies {
    lintChecks project(":compose:material3:material3-lint")
    lintPublish project(":compose:material3:material3-lint")
}

androidx {
    name = "Compose Material3 Components"
    type = LibraryType.PUBLISHED_LIBRARY_ONLY_USED_BY_KOTLIN_CONSUMERS
    inceptionYear = "2021"
    description = "Compose Material You Design Components library"
    legacyDisableKotlinStrictApiMode = true
    samples(project(":compose:material3:material3:material3-samples"))
}

// Screenshot tests related setup
android {
    sourceSets.androidTest.assets.srcDirs +=
            project.rootDir.absolutePath + "/../../golden/compose/material3/material3"
    namespace "androidx.compose.material3"
    // TODO(b/345531033)
    experimentalProperties["android.lint.useK2Uast"] = false
}
