/*
 * Copyright (C) 2023 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.android.build.api.artifact.SingleArtifact

plugins {
    id("AndroidXPlugin")
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
}

android {
    namespace "androidx.privacysandbox.sdkruntime.testsdk.v1"
}

dependencies {
    implementation("androidx.privacysandbox.sdkruntime:sdkruntime-core:1.0.0-alpha01")
}

/*
 * Allow integration tests to consume the APK produced by this project
 */
configurations {
    testSdkApk {
        canBeConsumed = true
        canBeResolved = false
        attributes {
            attribute(
                    LibraryElements.LIBRARY_ELEMENTS_ATTRIBUTE,
                    objects.named(LibraryElements, "testSdkApk")
            )
        }
    }
}

androidComponents {
    beforeVariants(selector().all()) { enabled = buildType == 'release' }
    onVariants(selector().all().withBuildType("release"), { variant ->
        artifacts {
            testSdkApk(variant.artifacts.get(SingleArtifact.APK.INSTANCE))
        }
    })
}