/*
 * Copyright 2018 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import androidx.build.Publish
import org.jetbrains.kotlin.gradle.plugin.mpp.KotlinNativeTarget

plugins {
    id("AndroidXPlugin")
    id("org.jetbrains.kotlin.multiplatform")
}

// This should be true when building from GitHub, and false when building
// from AOSP.  Use this to block out any features or code that we're not
// ready to build yet in AOSP

def boolProperty(name) {
    return project.providers.gradleProperty(name).getOrNull()?.toBoolean()
}

def playground = androidx.build.StudioType.isPlayground(project)
//Native on by default in playground
def enableNative = boolProperty('androidx.kmp.native.enabled') ?: playground

kotlin {
    jvm {
        withJava()
    }
    if (enableNative) {
        macosX64()
        macosArm64()
        linuxX64()
    }

    sourceSets {
        commonMain {
            dependencies {
                api(libs.kotlinStdlibCommon)
            }
        }
        commonTest {
            dependencies {
                implementation(libs.kotlinTestCommon)
                implementation(libs.kotlinTestAnnotationsCommon)
                implementation(libs.kotlinCoroutinesCore)
            }
        }

        if (enableNative) {
            nativeMain {
                dependsOn commonMain
            }

            configure([linuxX64Main, macosX64Main, macosArm64Main]) {
                dependsOn nativeMain
            }

            nativeTest {
                dependsOn commonTest
            }

            configure([linuxX64Test, macosX64Test, macosArm64Test]) {
                dependsOn nativeTest
            }
            macMain {
                dependsOn(nativeMain)
            }
            configure([macosArm64Main, macosX64Main]) {
                dependsOn macMain
            }
        }

        jvmMain {
            dependencies {
                api(libs.kotlinStdlib)
                api("androidx.annotation:annotation:1.3.0")
            }
        }
        jvmTest {
            dependencies {
                implementation(libs.kotlinTestJunit)
                implementation(libs.truth)
                implementation(project(":internal-testutils-truth"))
            }
        }

        all {
            languageSettings.optIn("kotlin.RequiresOptIn")
            languageSettings.optIn("kotlin.contracts.ExperimentalContracts")
        }
    }
}

androidx {
    name = "Android Support Library collections"
    publish = Publish.SNAPSHOT_AND_RELEASE
    mavenGroup = LibraryGroups.COLLECTION
    inceptionYear = "2018"
    description = "Standalone efficient collections."
    publishPlatforms = ["jvm"]
}

kotlin.targets.withType(KotlinNativeTarget.class) {
    binaries.all {
        binaryOptions["memoryModel"] = "experimental"
    }
}
