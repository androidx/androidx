/*
 * Copyright (C) 2017 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.Publish
import androidx.build.AndroidXConfig

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
}

dependencies {
    api("androidx.annotation:annotation:1.2.0")
    api("androidx.core:core:1.1.0")

    androidTestImplementation(libs.okhttpMockwebserver)
    androidTestImplementation(libs.testExtJunit)
    androidTestImplementation(libs.testCore)
    androidTestImplementation(libs.testRunner)
    androidTestImplementation(libs.testRules)
    androidTestImplementation("androidx.appcompat:appcompat:1.1.0")
    androidTestImplementation("androidx.concurrent:concurrent-futures:1.0.0")
    androidTestImplementation(libs.mockitoCore, excludes.bytebuddy) // DexMaker has it"s own MockMaker
    androidTestImplementation(libs.dexmakerMockito, excludes.bytebuddy)
    androidTestImplementation(libs.multidex)

    // Hamcrest matchers:
    androidTestImplementation(libs.bundles.espressoContrib, excludes.espresso)
}

ext {
    webviewBoundaryInterfacesDir = new File(AndroidXConfig.getExternalProjectPath(project),
            "webview_support_interfaces")
}

android {
    lintOptions {
        lintConfig = file("lint.xml")
        baseline file("lint-baseline.xml")
    }
    sourceSets {
        // Allow compiling the WebView support library boundary interfaces from this project.
        main.java.srcDirs += new File(webviewBoundaryInterfacesDir, "src").getCanonicalPath()
    }
    defaultConfig {
        multiDexEnabled = true
    }
    buildTypes.all {
        consumerProguardFiles(
                new File(webviewBoundaryInterfacesDir, "proguard.flags"),
                "proguard-rules.pro"
        )
    }
    namespace "androidx.webkit"
}

androidx {
    name = "WebKit"
    publish = Publish.SNAPSHOT_AND_RELEASE
    inceptionYear = "2017"
    description = "The WebKit Support Library is a static library you can add to your Android application in order to use android.webkit APIs that are not available for older platform versions."
    additionalDeviceTestApkKeys.add("chrome")
    metalavaK2UastEnabled = true
}
