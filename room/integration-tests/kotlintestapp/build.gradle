/*
 * Copyright (C) 2017 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
// TO debug processor, run:
//./gradlew :r:in:k:clean :r:in:k:cC --no-daemon
// -Dorg.gradle.debug=true
// -Dkotlin.compiler.execution.strategy="in-process"

import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id("AndroidXPlugin")
    id("com.android.application")
    id("kotlin-android")
}
// If set to `true`, we'll use KSP instead of KAPT.
// Note that the CI does not run tests with KSP yet so this is only for local usage.
// Once variants are properly supported by both ksp and AndroidX, we'll add support for this.
// (b/153917176)
def useKsp = project.properties.getOrDefault("useKsp", "false").toBoolean()
if (useKsp) {
    apply plugin: "com.google.devtools.ksp"
} else {
    apply plugin: "kotlin-kapt"
}

android {
    buildFeatures {
        buildConfig = true
    }
    defaultConfig {
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.expandProjection": "true"
                ]
            }
        }
        buildConfigField("boolean", "KSP", "$useKsp")
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    flavorDimensions "processorConfiguration"
    productFlavors {
        withExpandProjection {
            dimension "processorConfiguration"
            javaCompileOptions {
                annotationProcessorOptions {
                    arguments = [
                            "room.schemaLocation"  : "$projectDir/schemas".toString(),
                            "room.expandProjection"  : "true"
                    ]
                }
            }
        }
        withoutExpandProjection {
            dimension "processorConfiguration"
            javaCompileOptions {
                annotationProcessorOptions {
                    arguments = [
                            "room.schemaLocation"  : "$projectDir/schemas".toString(),
                            "room.expandProjection"  : "false"
                    ]
                }
            }
        }
    }
}

dependencies {
    implementation(project(":room:room-common"))
    implementation(project(":room:room-runtime"))
    implementation(project(":room:room-paging"))
    implementation(projectOrArtifact(":arch:core:core-runtime"))
    implementation(projectOrArtifact(":lifecycle:lifecycle-livedata"))
    implementation(projectOrArtifact(":lifecycle:lifecycle-livedata-ktx"))
    implementation(libs.kotlinStdlib)
    implementation(libs.kotlinCoroutinesAndroid)
    implementation(libs.multidex)
    // depend on the shadowed version so that it tests with the shipped artifact
    // this is a temporary attribute until KSP and AndroidX plugin supports variants.
    if (useKsp) {
        kspAndroidTest(
                project(path: ":room:room-compiler", configuration: "shadowAndImplementation")
        )
    } else {
        kaptAndroidTest(
                project(path: ":room:room-compiler", configuration: "shadowAndImplementation")
        )
    }
    androidTestImplementation(projectOrArtifact(":lifecycle:lifecycle-livedata-ktx"))
    androidTestImplementation(projectOrArtifact(":arch:core:core-runtime")) // Added for b/155802460
    androidTestImplementation(libs.testExtJunit)
    androidTestImplementation(libs.testCore)
    androidTestImplementation(libs.testRunner) {
        exclude module: "support-annotations"
        exclude module: "hamcrest-core"
    }
    androidTestImplementation(libs.espressoCore, {
        exclude group: "com.android.support", module: "support-annotations"
        exclude module: "hamcrest-core"
    })
    androidTestImplementation(libs.truth)
    androidTestImplementation(libs.kotlinTest)
    androidTestImplementation(project(":room:room-guava"))
    androidTestImplementation(project(":room:room-paging")) // Added for b/155802460
    androidTestImplementation(project(":room:room-testing"))
    androidTestImplementation(project(":room:room-rxjava2"))
    androidTestImplementation(project(":room:room-rxjava3"))
    androidTestImplementation(project(":room:room-ktx"))
    androidTestImplementation("androidx.arch.core:core-testing:2.0.1")
    androidTestImplementation("androidx.paging:paging-runtime:3.0.0")
    androidTestImplementation(libs.guavaAndroid)
    androidTestImplementation(libs.rxjava2)
    testImplementation(libs.mockitoCore)
}

if (useKsp) {
    ksp {
        arg("room.schemaLocation","$projectDir/schemas".toString())
        arg("room.expandProjection", "true")
    }
}

// Allow usage of Kotlin's @OptIn.
tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
    }
}
