/*
 * Copyright (C) 2017 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import static androidx.build.dependencies.DependenciesKt.*

buildscript {
    // TODO: Remove this when this test app no longer depends on 1.0.0 of vectordrawable-animated.
    // vectordrawable and vectordrawable-animated were accidentally using the same package name
    // which is no longer valid in namespaced resource world.
    project.ext['android.uniquePackageNames'] = false
}

plugins {
    id("AndroidXPlugin")
    id("com.android.application")
}

android {
    defaultConfig {
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.expandProjection": "true"
                ]
            }
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
        androidTest.assets.srcDirs += files("$projectDir/databases".toString())
    }
}

dependencies {
    implementation(project(":room:room-common"))
    implementation(project(":room:room-runtime"))
    implementation(project(":sqlite:sqlite"))
    implementation(project(":sqlite:sqlite-framework"))
    implementation(project(":arch:core-runtime"))
    implementation(project(":lifecycle:lifecycle-livedata"))

    // FINDBUGS dependency resolves an app/testapp version conflict.
    implementation(FINDBUGS)
    implementation(SUPPORT_RECYCLERVIEW, libs.support_exclude_config)
    implementation(SUPPORT_APPCOMPAT, libs.support_exclude_config)
    annotationProcessor project(":room:room-compiler")
    androidTestAnnotationProcessor project(":room:room-compiler")

    androidTestImplementation(project(":room:room-testing"))
    androidTestImplementation(project(":room:room-rxjava2"))
    androidTestImplementation(project(":room:room-guava"))
    androidTestImplementation(ARCH_CORE_TESTING)
    androidTestImplementation(ARCH_PAGING_RUNTIME)

    // FINDBUGS dependency resolves an app/testapp version conflict.
    androidTestImplementation(FINDBUGS)
    androidTestImplementation(GUAVA_ANDROID)
    androidTestImplementation(RX_JAVA)
    androidTestImplementation(ANDROIDX_TEST_EXT_JUNIT)
    androidTestImplementation(ANDROIDX_TEST_CORE)
    androidTestImplementation(ANDROIDX_TEST_RUNNER)
    androidTestImplementation(ANDROIDX_TEST_RULES)
    androidTestImplementation(ESPRESSO_CORE)
    androidTestImplementation(MOCKITO_CORE, libs.exclude_bytebuddy) // DexMaker has it's own MockMaker
    androidTestImplementation(DEXMAKER_MOCKITO, libs.exclude_bytebuddy) // DexMaker has it's own MockMaker

    testImplementation(JUNIT)
}
