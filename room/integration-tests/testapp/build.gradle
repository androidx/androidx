/*
 * Copyright (C) 2017 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'com.android.application'

project.ext.noDocs = true

android {
    compileSdkVersion tools.current_sdk
    buildToolsVersion tools.build_tools_version

    defaultConfig {
        minSdkVersion flatfoot.min_sdk
        targetSdkVersion tools.current_sdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        debug {
            // Use a local debug keystore to avoid build server issues.
            storeFile project.rootProject.init.debugKeystore
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

dependencies {
    implementation project(":room:common")
    implementation project(":persistence:db")
    implementation project(":persistence:db-framework")
    implementation project(':room:runtime')
    implementation project(':arch:runtime')
    implementation project(':arch:common')
    implementation project(':paging:common')
    implementation project(':lifecycle:extensions')
    implementation project(':lifecycle:runtime')
    implementation project(':lifecycle:common')
    implementation project(':room:rxjava2')
    implementation project(':paging:runtime')

    implementation libs.support.recyclerview, libs.support_exclude_config
    implementation libs.support.app_compat, libs.support_exclude_config
    annotationProcessor project(":room:compiler")
    androidTestAnnotationProcessor project(":room:compiler")

    // IJ's gradle integration just cannot figure this out ...
    androidTestImplementation project(':lifecycle:extensions')
    androidTestImplementation project(':lifecycle:common')
    androidTestImplementation project(':lifecycle:runtime')
    androidTestImplementation project(':room:testing')
    androidTestImplementation project(':room:rxjava2')
    androidTestImplementation project(':arch:core-testing')
    androidTestImplementation libs.rx_java
    androidTestImplementation(libs.test_runner) {
        exclude module: 'support-annotations'
    }
    androidTestImplementation(libs.espresso_core, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation libs.mockito_core,     { exclude group: 'net.bytebuddy' } // DexMaker has it's own MockMaker
    androidTestImplementation libs.dexmaker_mockito, { exclude group: 'net.bytebuddy' } // DexMaker has it's own MockMaker

    testImplementation libs.junit
}

tasks['check'].dependsOn(tasks['connectedCheck'])

uploadArchives.enabled = false
