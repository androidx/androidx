/*
 * Copyright 2018 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    // TODO: Remove this when this test app no longer depends on 1.0.0 of vectordrawable-animated.
    // vectordrawable and vectordrawable-animated were accidentally using the same package name
    // which is no longer valid in namespaced resource world.
    project.ext["android.uniquePackageNames"] = false
}

plugins {
    id("AndroidXPlugin")
    id("com.android.application")
}

dependencies {
    implementation(project(":room:room-common"))
    implementation(project(":room:room-runtime"))
    implementation("androidx.arch.core:core-runtime:2.2.0")

    // depend on the shadowed version so that it tests with the shipped artifact
    androidTestAnnotationProcessor project(path: ":room:room-compiler",
            configuration: "shadowAndImplementation")
    androidTestAnnotationProcessor(libs.autoValue)
    androidTestAnnotationProcessor(libs.autoValueParcel)

    androidTestImplementation(project(":room:room-testing"))
    androidTestImplementation("androidx.arch.core:core-testing:2.2.0")
    androidTestImplementation(libs.autoValueAnnotations)
    androidTestImplementation(libs.testExtJunit)
    androidTestImplementation(libs.testCore)
    androidTestImplementation(libs.testRunner)
    androidTestImplementation(libs.espressoCore)
    androidTestImplementation(libs.mockitoCore, excludes.bytebuddy) // DexMaker has it's own MockMaker
    androidTestImplementation(libs.dexmakerMockito, excludes.bytebuddy) // DexMaker has it's own MockMaker

    testImplementation(libs.junit)
}

android {
    namespace "androidx.room.integration.autovaluetestapp"
}

// Enable parameter names to support Room incremental when its a project() dep.
// See b/198431380
tasks.withType(JavaCompile) {
    options.compilerArgs << "-parameters"
}
