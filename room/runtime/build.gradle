/*
 * Copyright (C) 2016 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import static android.support.dependencies.DependenciesKt.*
import android.support.LibraryGroups
import android.support.LibraryVersions
import android.support.SupportLibraryExtension

plugins {
    id("SupportAndroidLibraryPlugin")
}

android {
    buildTypes.all {
        consumerProguardFiles 'proguard-rules.pro'
    }
}

dependencies {
    api(project(":room:common"))
    api(project(":persistence:db-framework"))
    api(project(":persistence:db"))
    api(project(":arch:runtime"))
    compileOnly project(":paging:common")
    compileOnly project(":lifecycle:runtime")
    compileOnly project(":lifecycle:extensions")
    api(SUPPORT_CORE_UTILS, libs.support_exclude_config)

    testImplementation(project(":arch:core-testing"))
    testImplementation(JUNIT)
    testImplementation(MOCKITO_CORE)

    androidTestImplementation(JUNIT)
    androidTestImplementation(TEST_RUNNER)
    androidTestImplementation(ESPRESSO_CORE)
}

// Used by testCompile in room-compiler
android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    def suffix = name.capitalize()
    project.tasks.create(name: "jar${suffix}", type: Jar){
        dependsOn variant.javaCompile
        from variant.javaCompile.destinationDir
        destinationDir new File(project.buildDir, "libJar")
    }
}

supportLibrary {
    name = "Android Room-Runtime"
    publish = true
    mavenVersion = LibraryVersions.ROOM
    mavenGroup = LibraryGroups.ROOM
    inceptionYear = "2017"
    description = "Android Room-Runtime"
    url = SupportLibraryExtension.ARCHITECTURE_URL
}