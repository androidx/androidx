apply plugin: android.support.SupportLibraryPlugin
apply plugin: 'org.anarres.jarjar'
archivesBaseName = 'support-emoji'

ext {
    File externalRoot = new File(rootDir, '../../external')
    emojiCompatFontDir = externalRoot.canonicalPath + '/noto-fonts/emoji-compat'
}

dependencies {
    compile project(':support-compat')

    compile jarjar.repackage {
        from files("$emojiCompatFontDir/libs/flatbuffers-java-1.6.0.jar")
        destinationName 'flatbuffers-java-1.6.0-repacked.jar'
        classRename 'com.google.flatbuffers.**', 'android.support.text.emoji.flatbuffer.@1'
    }

    androidTestCompile (libs.test_runner) {
        exclude module: 'support-annotations'
    }
    androidTestCompile (libs.espresso_core) {
        exclude module: 'support-annotations'
    }
    androidTestCompile libs.mockito_core
    androidTestCompile libs.dexmaker
    androidTestCompile libs.dexmaker_mockito
    androidTestCompile project(path: ':support-testutils')
}

android {
    compileSdkVersion project.ext.currentSdk

    defaultConfig {
        minSdkVersion 19
    }

    sourceSets {
        main.java {
            srcDirs = ['src', "$emojiCompatFontDir/src/java"]
        }
        main.resources {
            srcDirs = ["$emojiCompatFontDir"]
            includes = ["LICENSE_UNICODE", "LICENSE_OFL"]
        }

        androidTest {
            assets {
                srcDirs = ["$emojiCompatFontDir/font", "$emojiCompatFontDir/supported-emojis"]
            }
        }
    }
}

supportLibrary {
    name 'Android Emoji Compat'
    inceptionYear '2017'
    description 'Core library to enable emoji compatibility in Kitkat and newer devices to avoid the empty emoji characters.'

    license {
        name 'SIL Open Font License, Version 1.1'
        url  'http://scripts.sil.org/cms/scripts/page.php?item_id=OFL_web'
    }

    license {
        name 'Unicode, Inc. License'
        url 'http://www.unicode.org/copyright.html#License'
    }
}
