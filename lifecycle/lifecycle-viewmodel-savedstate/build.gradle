/*
 * Copyright 2018 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */

import androidx.build.JetbrainsAndroidXPlugin
import androidx.build.PlatformIdentifier
import androidx.build.Publish
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("JetbrainsAndroidXPlugin")
}

JetbrainsAndroidXPlugin.applyAndConfigure(project)

androidXMultiplatform {
    android()
    desktop()
    mac()
    linux()
    ios()
    js()
    wasm()

    defaultPlatform(PlatformIdentifier.ANDROID)
}

kotlin {
    js {
        browser()
    }
    wasmJs()

    sourceSets {
        commonMain {
            dependencies {
                api(project(":annotation:annotation"))
                api(project(":core:core-bundle"))
                api(project(":lifecycle:lifecycle-common"))
                api(project(":lifecycle:lifecycle-viewmodel"))
                api(project(":savedstate:savedstate"))
                api(libs.kotlinStdlib)
            }
        }

        jvmMain {
            dependsOn(commonMain)
        }

        androidMain {
            dependsOn(jvmMain)
            dependencies {
                api("androidx.core:core-ktx:1.2.0")
                api("androidx.lifecycle:lifecycle-livedata-core:2.7.0")
                api(libs.kotlinCoroutinesAndroid)
            }
        }

        androidInstrumentedTest {
            dependencies {
                implementation project(":lifecycle:lifecycle-runtime")
                implementation("androidx.fragment:fragment:1.3.0")
                implementation project(":internal-testutils-runtime")
                implementation(libs.truth)
                implementation(libs.kotlinStdlib)
                implementation(libs.testExtJunit)
                implementation(libs.testCore)
                implementation(libs.testRunner)
                implementation(libs.testRules)
            }
        }

        jbMain.dependsOn(commonMain)
        nonJvmMain.dependsOn(jbMain)
        desktopMain {
            dependsOn(jvmMain)
            dependsOn(jbMain)
        }

        targets.all { target ->
            if (target.platformType !in [
                    KotlinPlatformType.androidJvm,
                    KotlinPlatformType.jvm,
                    KotlinPlatformType.common
            ]) {
                target.compilations["main"].defaultSourceSet {
                    dependsOn(nonJvmMain)
                }
            }
        }
    }
}

android {
    buildTypes.all {
        consumerProguardFiles("proguard-rules.pro")
    }
    namespace "androidx.lifecycle.viewmodel.savedstate"
}

androidx {
    name = "Lifecycle ViewModel with SavedState"
    publish = Publish.SNAPSHOT_AND_RELEASE
    inceptionYear = "2018"
    description = "Android Lifecycle ViewModel"
    metalavaK2UastEnabled = true
}
