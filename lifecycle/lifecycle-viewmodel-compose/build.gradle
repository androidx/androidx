/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */

import androidx.build.JetbrainsAndroidXPlugin
import androidx.build.AndroidXComposePlugin
import androidx.build.Publish
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
    id("JetbrainsAndroidXPlugin")
}

AndroidXComposePlugin.applyAndConfigureKotlinPlugin(project)
JetbrainsAndroidXPlugin.applyAndConfigure(project)

androidXComposeMultiplatform {
    android()
    desktop()
    darwin()
    js()
    wasm()
}

kotlin {
    sourceSets {
        commonMain {
            dependencies {
                api project(":lifecycle:lifecycle-common")
                api project(":lifecycle:lifecycle-viewmodel")
                api project(":lifecycle:lifecycle-viewmodel-savedstate")
                api project(":savedstate:savedstate")
                api project(":compose:runtime:runtime")
                api project(":compose:runtime:runtime-saveable")
                api project(":compose:ui:ui")

                implementation(libs.kotlinStdlib)
                implementation project(":core:core-bundle")
            }
        }

        jvmMain.dependsOn(commonMain)
        androidMain.dependsOn(jvmMain)

        androidInstrumentedTest {
            dependencies {
                implementation project(":lifecycle:lifecycle-runtime")
                implementation("androidx.fragment:fragment:1.3.0")
                implementation project(":compose:test-utils")
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
            }
        }

        jbMain.dependsOn(commonMain)
        desktopMain {
            dependsOn(jvmMain)
            dependsOn(jbMain)
        }
        nonJvmMain.dependsOn(jbMain)
        nativeMain.dependsOn(nonJvmMain)
        webMain.dependsOn(nonJvmMain)

        targets.all { target ->
            if (target.platformType == KotlinPlatformType.native) {
                target.compilations["main"].defaultSourceSet {
                    dependsOn(nativeMain)
                }
            } else if (target.platformType in [
                    KotlinPlatformType.js,
                    KotlinPlatformType.wasm
            ]) {
                target.compilations["main"].defaultSourceSet {
                    dependsOn(webMain)
                }
            }
        }
    }
}

androidx {
    name = "Lifecycle ViewModel Compose"
    publish = Publish.SNAPSHOT_AND_RELEASE
    inceptionYear = "2021"
    description = "Compose integration with Lifecycle ViewModel"
    metalavaK2UastEnabled = true
}

android {
    namespace "androidx.lifecycle.viewmodel.compose"
}
