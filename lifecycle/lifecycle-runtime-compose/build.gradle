/*
 * Copyright 2022 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */
import androidx.build.LibraryType
import androidx.build.PlatformIdentifier

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

androidXMultiplatform {
    android()
    desktop()

    defaultPlatform(PlatformIdentifier.ANDROID)

    sourceSets {
        commonMain {
            dependencies {
                api(projectOrArtifact(":lifecycle:lifecycle-runtime"))
                api("androidx.annotation:annotation:1.8.0")
                api("androidx.compose.runtime:runtime:1.6.5")
            }
        }

        androidMain {
            dependsOn(commonMain)
            dependencies {
                // Although this artifact is empty, it ensures that upgrading
                // `lifecycle-runtime-compose` also updates `lifecycle-runtime-ktx`
                // in cases where our constraints fail (e.g., internally in AndroidX
                // when using project dependencies).
                api(projectOrArtifact(":lifecycle:lifecycle-runtime-ktx"))
            }
        }

        androidInstrumentedTest {
            dependencies {
                implementation(projectOrArtifact(":lifecycle:lifecycle-runtime-testing"))
                implementation(projectOrArtifact(":compose:ui:ui-test"))
                implementation(project(":compose:test-utils"))
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.kotlinTest)
                implementation(project(":kruth:kruth"))
            }
        }
    }
}

dependencies {
    // Starting from Lifecycle 2.8.0-alpha03 and Compose UI 1.7.0-alpha05,
    // LocalLifecycleOwner is located in lifecycle-runtime-compose. Using
    // Lifecycle 2.8.0+ with Compose UI 1.6 or older may cause runtime
    // issues due to this change.
    constraints {
        commonMainImplementation("androidx.compose.ui:ui:1.7.0-beta01") {
            because 'prevents a runtime error with LocalLifecycleOwner'
        }
    }
}

androidx {
    name = "Lifecycle Runtime Compose"
    type = LibraryType.PUBLISHED_LIBRARY_ONLY_USED_BY_KOTLIN_CONSUMERS
    inceptionYear = "2021"
    description = "Compose integration with Lifecycle"
    samples(project(":lifecycle:lifecycle-runtime-compose:lifecycle-runtime-compose-samples"))
}

android {
    buildTypes.configureEach {
        consumerProguardFiles "proguard-rules.pro"
    }

    namespace "androidx.lifecycle.runtime.compose"
}
