/*
 * Copyright (C) 2021 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */
import androidx.build.LibraryType
import androidx.build.Publish

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("org.jetbrains.kotlin.android")
}

dependencies {
    api(libs.kotlinStdlib)
    implementation(libs.kotlinCoroutinesAndroid)
    implementation("androidx.annotation:annotation-experimental:1.4.1")
    implementation("androidx.core:core:1.8.0")
    androidTestImplementation(libs.testExtJunit)
    androidTestImplementation(libs.testCore)
    androidTestImplementation(libs.testRunner)
    androidTestImplementation(libs.testRules)
    androidTestImplementation("androidx.lifecycle:lifecycle-common:2.8.3")
    androidTestImplementation("androidx.test:core:1.4.0@aar")
}

android {
    compileSdk 35

    namespace 'androidx.graphics.core'

    defaultConfig {
        externalNativeBuild {
            def versionScript = file("src/main/cpp/jni.lds").getAbsolutePath()

            cmake {
                cppFlags "-O3", "-flto", "-fPIC", "-fno-exceptions", "-fno-rtti",
                        "-fomit-frame-pointer", "-fdata-sections", "-ffunction-sections", "-fvisibility=hidden"
                arguments "-DCMAKE_VERBOSE_MAKEFILE=ON",
                        "-DCMAKE_SHARED_LINKER_FLAGS=-Wl,--gc-sections " +
                                "-Wl,--version-script=${versionScript} " +
                                "-Wl,--undefined-version"
            }
        }
        consumerProguardFiles 'proguard-rules.pro'
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }
}

androidx {
    name = "Graphics Core"
    type = LibraryType.PUBLISHED_LIBRARY
    mavenVersion = LibraryVersions.GRAPHICS_CORE
    inceptionYear = "2021"
    description = "Leverage graphics facilities across multiple Android platform releases"
    legacyDisableKotlinStrictApiMode = true
    samples(project(":graphics:graphics-core:graphics-core-samples"))
}
