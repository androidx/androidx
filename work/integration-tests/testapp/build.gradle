/*
 * Copyright (C) 2016 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import static androidx.build.dependencies.DependenciesKt.CONSTRAINT_LAYOUT

buildscript {
    // TODO: Remove this when this test app no longer depends on 1.0.0 of vectordrawable-animated.
    // vectordrawable and vectordrawable-animated were accidentally using the same package name
    // which is no longer valid in namespaced resource world.
    project.ext["android.uniquePackageNames"] = false
}

plugins {
    id("AndroidXPlugin")
    id("com.android.application")
    id("kotlin-android")
}

android {
    buildTypes {
        getByName("release") {
            minifyEnabled = true
        }
    }
    defaultConfig {
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"
                ]
            }
        }
        vectorDrawables {
            useSupportLibrary = true
        }
    }
}

dependencies {
    // Using -Pandroidx.useMaxDepVersions does not use the right version of the annotation processor
    // Remove this workaround after b/127495641 is fixed
    if (project.hasProperty("androidx.useMaxDepVersions")) {
        annotationProcessor(projectOrArtifact(":room:room-compiler"))
        implementation(projectOrArtifact(":room:room-runtime"))
    } else {
        annotationProcessor("androidx.room:room-compiler:2.2.5")
        implementation("androidx.room:room-runtime:2.2.5")
    }

    implementation(CONSTRAINT_LAYOUT, { transitive = true })
    implementation("androidx.lifecycle:lifecycle-service:2.2.0")
    implementation(project(":work:work-runtime-ktx"))
    implementation(project(":work:work-multiprocess"))
    implementation(project(":work:work-gcm"))
    implementation("androidx.concurrent:concurrent-futures-ktx:1.1.0")
    implementation("androidx.arch.core:core-runtime:2.1.0")
    implementation("androidx.recyclerview:recyclerview:1.1.0")
    implementation(libs.material)
}
